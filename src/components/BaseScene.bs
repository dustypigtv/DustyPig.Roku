Sub Init()

    print "BaseScene:Init"

    m.loggedIn = false

    ' Landing view
    m.landingView = m.top.findNode("landingView")

    ' Login View
    m.loginView = m.top.findNode("loginView")
    m.loginView.observeField("switchToSignup", "SwitchToSignUpView")
    m.loginView.observeField("switchToSelectProfile", "SwitchToSelectProfileView")
    m.loginView.observeField("switchToHomeScreen", "SwitchToHomeView")


    ' Signup View
    m.signupView = m.top.findNode("signupView")
    m.signupView.observeField("switchToLogin", "SwitchToLoginView")
    m.signupView.observeField("switchToSelectProfile", "SwitchToSelectProfileView")
    m.loginView.observeField("switchToHomeScreen", "SwitchToHomeView")


    ' Select Profile View
    m.selectProfileView = m.top.findNode("selectProfileView")
    m.selectProfileView.observeField("cancel", "CancelSelectProfileView")
    m.selectProfileView.observeField("switchToHomeView", "SwitchToHomeView")
    m.selectProfileView.observeField("switchToLoginView", "SwitchToLoginView")


    ' Home View
    m.homeView = m.top.findNode("homeView")
    m.homeView.observeField("switchToLoginView", "SwitchToLoginView")


    m.viewStack = []
    ShowView(m.landingView)
    
    

    ' Do this 2nd to last
    m.AuthTask = CreateObject("roSGNode", "AuthenticationTask")
    m.AuthTask.observeField("startAuthFlow", "SwitchToLoginView")
    m.AuthTask.observeField("startProfileFlow", "SwitchToSelectProfileView")
    m.AuthTask.Control = "RUN"

    ' Do this LAST
    m.top.signalBeacon("AppLaunchComplete")

End Sub


Sub ShowView(node)

    prev = m.viewStack.peek()
    if prev <> invalid then prev.visible = false    
    node.visible = true
    node.setFocus(true)
    m.viewStack.push(node)    

End Sub

Sub HideTopView()

   HideView(invalid)

end Sub

Sub HideView(node as Object)

    if node = invalid OR (m.viewStack.peek() <> invalid AND m.viewStack.peek().isSameNode(node))     
        last = m.viewStack.pop()
        last.visible = false        
        prev = m.viewStack.peek()
        if prev <> invalid then
            prev.visible = true
            prev.setFocus(true)    
        end if    
    end if

End Sub




Sub SwitchToLoginView()

    print "BaseScene:SwitchToLoginView"

    m.global.accountAuthorized = false
    m.loggedIn = false
    ShowView(m.loginView)

End Sub

Sub SwitchToSignUpView()

    print "BaseScene:SwitchToSignUpView"

    m.global.accountAuthorized = false
    m.loggedIn = false
    ShowView(m.signupView)

End Sub


Sub SwitchToSelectProfileView()

    print "BaseScene:SwitchToSelectProfileView"

    ShowView(m.selectProfileView)

End Sub

Sub SwitchToHomeView()

    print "BaseScene:SwitchToHomeView"

    m.loggedIn = true
    ShowView(m.homeView)

End Sub

sub CancelSelectProfileView()

    print "BaseScene:CancelSelectProfileView"

    if m.loggedIn then
        ShowView(m.homeView)
    else
        ShowView(m.loginView)
    end if

end sub
