Sub Init()

    m.top.functionName = "RunTask"

    End Sub


Sub RunTask()

    m.top.unauthorized = false

    result = GetData("/Media/HomeScreen?itemsPerSection=100", m.global.profileToken)
        
    if result.success then

        ' Create a root content node, with child nodes holding info
        root = createObject("roSGNode", "ContentNode")
        for each section in result.data.sections
            
            row = root.createChild("ContentNode")
            row.id = section.listId
            row.title = section.title
            
            for each bm in section.items

                item = row.createChild("ContentNode")
                item.id = bm.id
                item.title = bm.title
                item.description = bm.description
                item.FHDGRIDPOSTERURL = bm.artworkUrl
                item.addFields({backdropUrl: bm.backdropUrl, mediaType: bm.mediaType})
                
            end for
            
        end for

        

        if m.top.data = invalid then
        
            m.top.data = root
        
        else
        
            ' Updating nodes in place provides the least amount of flickering
            ' But first a quick scan to see if anything changed
            ' Scan id's in positions, and for the playlist row also can posters

            current = m.top.data
            if AreNodesTheSame(current, root) then return
            

            ' At this point, do in-place updates
            while current.getChildCount() > root.getChildCount()
                current.removeChildIndex(current.getChildCount() - 1)
            end while

            for i = 0 to root.getChildCount() - 1
                
                newSection = root.getChild(i)

                if i < current.getChildCount() then

                    curSection = current.getChild(i)

                    if newSection.id <> curSection.id then

                        ' Things like replaceChild or insertChild are throwing. So just update it
                        CopySectionFields(newSection, curSection)
                    
                    end if

                    while curSection.getChildCount() > newSection.getChildCount()
                        curSection.removeChildIndex(curSection.getChildCount() - 1)
                    end while
                
                    for j = 0 to newSection.getChildCount() - 1
                
                        newItem = newSection.getChild(j)
                
                        if j < curSection.getChildCount() then
                
                            curItem = curSection.getChild(j)
                
                            if newItem.id <> curItem.id then 
                
                                ' Things like replaceChild or insertChild are throwing. So just update it
                                CopyItemFields(newItem, curItem)
                
                            end if
                
                        else
                
                            ' curSection.appendChild(newItem)
                            ' appendChild(newItem) MOVES the child from 1 parent to another, screwing up the update
                            ' So instead manually create a new one

                            curItem = curSection.createChild("ContentNode")
                            CopyItemFields(newItem, curItem)
                            
                        end if
                
                    end for

                else
                   
                    ' current.appendChild(newSection)
                    ' appendChild(newSection) MOVES the child from 1 parent to another, screwing up the update
                    ' So instead manually create a new one

                    curSection = current.createChild("ContentNode")
                    CopySectionFields(newSection, curSection)
                
                end if

            end for
            
        end if

       
        m.top.success = true
        
    else if result.statusCode = 401 then

        m.top.unauthorized = true
        m.top.success = false

    else

        m.top.errMsg = result.error
        m.top.success = false
        
    end if

End Sub


Function AreNodesTheSame(src as roSGNode, dst as roSGNode) as boolean

    if src.getChildCount() <> dst.getChildCount() then return false

    for i = 0 to src.getChildCount() - 1

        srcSection = src.getChild(i)
        dstSection = dst.getChild(i)
        if srcSection.id <> dstSection.id then return false
        if srcSection.getChildCount() <> dstSection.getChildCount() then return false

        for j = 0 to srcSection.getChildCount() - 1

            srcItem = srcSection.getChild(j)
            dstItem = dstSection.getChild(j)
            if srcItem.id <> dstItem.id then return false

            ' public const long ID_WATCHLIST = -400;
            if srcSection.id = "-400" then
                if srcItem.FHDGRIDPOSTERURL <> dstItem.FHDGRIDPOSTERURL then return false
            end if

        end for

    end for

    return true

End Function

Sub CopySectionFields(srcSection as roSGNode, dstSection as roSGNode)

    dstSection.setFields({
        id:srcSection.id,
        title:srcSection.title
    })

End Sub


Sub CopyItemFields(srcItem as roSGNode, dstItem as roSGNode)

    dstItem.setFields({
        id:srcItem.id,
        title:srcItem.title,
        description:srcItem.description,
        FHDGRIDPOSTERURL:srcItem.FHDGRIDPOSTERURL,
        backdropUrl:srcItem.backdropUrl,
        mediaType:srcItem.mediaType
    })

End Sub