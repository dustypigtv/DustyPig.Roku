Sub Init()

    print "HomeScreenTask:Init"

    m.top.functionName = "RunTask"

    End Sub


Sub RunTask()

    print "HomeScreenTask:RunTask"

    result = GetData("/Media/HomeScreen?itemsPerSection=100", m.global.profileToken)
        
    if result.statusCode = 401 then
        m.global.loggedOut = true
        return
    end if


    if result.success then

        if m.top.data = invalid then

            ' Create a root content node, with child nodes holding sections, and each section holding BasicMedia nodes
            root = createObject("roSGNode", "ContentNode")
            for each section in result.data.sections
                
                row = root.createChild("ContentNode")
                row.id = section.listId.toStr()
                row.title = section.title
                
                for each bm in section.items

                    bm.id = bm.id.toStr()
                    item = row.createChild("BasicMediaNode")
                    item.data = bm
                    
                end for
                
            end for

            m.top.data = root
            m.top.success = true

        else
        
            ' Updating nodes in place provides the least amount of flickering
           
            currentData = m.top.data
            newData = result.data.sections

            ' If there are more current sections than new ones, trim
            while currentData.getChildCount() > newData.getChildCount()
                currentData.removeChildIndex(currentData.getChildCount() - 1)
            end while

            for i = 0 to newData.getChildCount() - 1
                
                newSection = newData.getChild(i)
                newSection.id = newSection.id.toStr()

                if i < currentData.getChildCount() then

                    ' Check if the section needs to update
                    curSection = currentData.getChild(i)
                    if newSection.id <> curSection.id then CopySectionFields(newSection, curSection)

                    ' If there are more current items in the section than the new ones, trim
                    while curSection.getChildCount() > newSection.getChildCount()
                        curSection.removeChildIndex(curSection.getChildCount() - 1)
                    end while
                
                    for j = 0 to newSection.getChildCount() - 1
                
                        newItem = newSection.getChild(j)
                        newItem.id = newItem.id.toStr()

                        if j < curSection.getChildCount() then
                            ' Check if the item needs to update
                            curItem = curSection.getChild(j)
                            if newItem.id <> curItem.id then curItem.data = newItem
                        else
                            ' Need to append a new item to the section
                            curItem = curSection.createChild("BasicMediaNode")
                            curItem.data = newItem
                        end if
                
                    end for

                else
                    ' Need to append new section
                    curSection = currentData.createChild("ContentNode")
                    CopySectionFields(newSection, curSection)
                end if

            end for
            
        end if

        m.top.success = true
               
   else

        m.top.errMsg = result.error
        m.top.success = false
        
    end if

End Sub


Sub CopySectionFields(srcSection as roSGNode, dstSection as roSGNode)

    print "HomeScreenTask:CopySectionFields"

    dstSection.setFields({
        id: srcSection.id,
        title: srcSection.title
    })

End Sub

