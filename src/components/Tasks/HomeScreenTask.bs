Sub Init()

    print "HomeScreenTask:Init"

    m.top.functionName = "TryGetData"

End Sub


Sub TryGetData()

    print "HomeScreenTask:TryGetData"

    m.top.unauthorized = false
        
    apiInstance = new API()
    result = apiInstance.GetData("/Media/HomeScreen", m.global.profileToken)
        
    if result.success then
    
        ' Create a root content node, with child nodes holding info
        root = createObject("roSGNode", "ContentNode")
        numRows = 0
        for each section in result.data.sections
            
            numRows++
            row = root.createChild("ContentNode")
            row.id = section.listId
            row.title = section.title

            for each bm in section.items

                item = row.createChild("ContentNode")
                item.id = bm.id
                item.title = bm.title
                item.FHDGRIDPOSTERURL = bm.artworkUrl
                item.addFields({backdropUrl: bm.backdropUrl, mediaType: bm.mediaType})
                
            end for
            
        end for
        
        m.top.data = root
        m.top.numRows = numRows
        m.top.success = true

    else if result.statusCode = 401 then

        m.top.unauthorized = true
        m.top.success = false

    else

        m.top.errMsg = result.error
        m.top.success = false

    end if

End Sub