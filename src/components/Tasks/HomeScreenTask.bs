Sub Init()

    m.top.functionName = "RunTask"

End Sub


Sub RunTask()

    result = GetData("/Media/HomeScreen?itemsPerSection=100", m.global.profileToken)
        
    if result.statusCode = 401 then
        m.global.loggedOut = true
        return
    end if


    if result.success then

        ' Create a root content node, with child nodes holding sections, and each section holding BasicMedia nodes
        root = createObject("roSGNode", "ContentNode")
        for each section in result.data.sections

            row = root.createChild("ContentNode")
            row.setFields({
                id: section.listid.ToStr(),
                title: section.title
            })
            
            for each bm in section.items
                SetBasicMediaFields(bm, row.createChild("BasicMediaNode"))
            end for
            
        end for

        m.top.data = root
        m.top.success = true

   else

        m.top.error = result.error
        m.top.success = false
        
    end if

End Sub
