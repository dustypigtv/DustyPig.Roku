Sub Init()
    m.top.functionName = "RunTask"
End Sub


Sub RunTask()

    m.top.unauthorized = false
        
    result = GetData("/Media/HomeScreen", m.global.profileToken)
        
    if result.success then
    
        ' Create a root content node, with child nodes holding info
        root = createObject("roSGNode", "ContentNode")
        for each section in result.data.sections
            
            row = root.createChild("ContentNode")
            row.id = section.listId
            row.title = section.title

            for each bm in section.items

                item = row.createChild("ContentNode")
                item.id = bm.id
                item.title = bm.title
                item.description = bm.description
                item.FHDGRIDPOSTERURL = bm.artworkUrl
                item.addFields({backdropUrl: bm.backdropUrl, mediaType: bm.mediaType})
                
            end for
            
        end for
        
        m.top.data = root
        m.top.success = true

    else if result.statusCode = 401 then

        m.top.unauthorized = true
        m.top.success = false

    else

        m.top.errMsg = result.error
        m.top.success = false

    end if

End Sub