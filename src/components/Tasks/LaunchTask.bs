Sub Init()

    print "LaunchTask:Init"

    m.top.functionName = "RunTask"

End Sub


Sub RunTask()

    print "LaunchTask:RunTask"
    
    ' DeleteAccountToken()

    result = CheckAccountToken()
    if result then
        profiles = GetData("/Profiles/List", GetAccountToken())
        if profiles.success then
            if profiles.data.count() = 1 then

                ' Only 1 profile, login
                di=createobject("roDeviceInfo")
                deviceId=di.GetChannelClientId()
                creds = {"id": profiles.data[0].id, "deviceId":deviceId }
                result = PostAndGetData("/Auth/ProfileLogin", creds, GetAccountToken())

                if result.success then

                    m.global.profileId = result.data.profileId
                    m.global.profileToken = result.data.profileToken
                    m.global.profileIsMain = true

                    ' Set theme AFTER setting profileId
                    m.global.theme = GetTheme()
                    m.top.switchToMainView = true

                else

                    ' Proile login failed
                    m.top.startAuthFlow = true

                end if

            else

                ' More than 1 profile
                m.global.allProfiles = ProfilesToNode(profiles.data)
                m.top.startProfileFlow = true
            
            end if
        else
            
            ' Loading profiles failed
            m.top.startAuthFlow = true
        
        end if
    else
        
        ' Auth with account token failed
        m.top.startAuthFlow = true
    
    end if

End Sub