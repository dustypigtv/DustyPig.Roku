Sub Init()
    m.top.functionName = "RunTask"
End Sub


Sub RunTask()

    DeleteAccountToken()

    result = CheckAccountToken()
    if result then
        profiles = GetData("/Profiles/List", GetAccountToken())
        if profiles.success then
            if profiles.data.count() = 1 then

                ' Only 1 profile, login
                di=createobject("roDeviceInfo")
                deviceId=di.GetChannelClientId()
                creds = {"id": profiles.data[0].id, "deviceId":deviceId }
                result = PostAndGetData("/Auth/ProfileLogin", creds, GetAccountToken())

                if result.success then

                    m.global.profileId = result.data.profileId
                    m.global.profileToken = result.data.token
                    m.global.profileIsMain = true

                    ' Set theme AFTER setting profileId
                    m.global.theme = GetTheme()

                    ' Get the home screen
                    result = GetData("/Media/HomeScreen", m.global.profileToken)
                    if result.success then
                    
                        ' Create a root content node, with child nodes holding info
                        root = createObject("roSGNode", "ContentNode")
                        for each section in result.data.sections
                            
                            row = root.createChild("ContentNode")
                            row.id = section.listId
                            row.title = section.title
                
                            for each bm in section.items
                
                                item = row.createChild("ContentNode")
                                item.id = bm.id
                                item.title = bm.title
                                item.description = bm.description
                                item.FHDGRIDPOSTERURL = bm.artworkUrl
                                item.addFields({backdropUrl: bm.backdropUrl, mediaType: bm.mediaType})
                                
                            end for
                            
                        end for
                        
                        m.top.homeScreenData = root
                        m.top.switchToMainView = true
                
                    else if result.statusCode = 401 then
                
                        ' Logged out while trying to get home screen. Probably will never happen, but catch it anyway
                        m.top.startAuthFlow = true
                
                    else
                
                         ' Failed to load the home screen, start over
                         m.top.startAuthFlow = true
                
                    end if

                else

                    ' Proile login failed
                    m.top.startAuthFlow = true

                end if

            else

                ' More than 1 profile
                m.global.allProfiles = ProfilesToNode(profiles.data)
                m.top.startProfileFlow = true
            
            end if
        else
            
            ' Loading profiles failed
            m.top.startAuthFlow = true
        
        end if
    else
        
        ' Auth with account token failed
        m.top.startAuthFlow = true
    
    end if

End Sub