Sub Init()

    m.top.functionName = "RunTask"

End Sub


Sub RunTask()

    ' DeleteAccountToken()

    ' sleep(3000)
    result = CheckAccountToken()
    if result then
        profiles = GetData("/Profiles/List", GetAccountToken())
        if profiles.success then
            if profiles.data.count() = 1 then

                ' Only 1 profile, login
                di = createobject("roDeviceInfo")
                deviceId = di.GetChannelClientId()
                creds = {"id": profiles.data[0].id, "deviceId":deviceId }
                result = PostAndGetData("/Auth/ProfileLogin", creds, GetAccountToken())

                if result.success then

                    m.global.profileId = result.data.profileId
                    m.global.profileToken = result.data.profileToken
                    m.global.profileIsMain = true

                    ' Set theme AFTER setting profileId
                    m.global.theme = GetTheme()
                    
                    m.global.loggedOut = false

                else

                    ' Proile login failed
                    m.global.loggedOut = true

                end if

            else

                ' More than 1 profile
                m.global.allProfiles = ProfilesToNode(profiles.data)
                m.global.showViewWithId = "loginProfileView"
            
            end if
        else
            
            ' Loading profiles failed
            m.global.loggedOut = true
        
        end if
    else
        
        ' Auth with account token failed
        m.global.loggedOut = true
    
    end if

End Sub