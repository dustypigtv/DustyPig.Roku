sub Init()

    m.top.functionName = "RunTask"

end sub

sub RunTask()

    di = createobject("roDeviceInfo")
    deviceId = di.GetChannelClientId()

    if m.top.pin = invalid or m.top.pin = "" then 
        creds = {id: m.top.id, deviceId: deviceId }
    else
        creds = {id: m.top.id, pin: m.top.pin, deviceId: deviceId}
    end if

    result = PostAndGetData("/Auth/ProfileLogin", creds, GetAccountToken())

    ' This could only happen during some unlikely timing
    if result.statusCode = 401 then
        m.global.loggedOut = true
        return
    end if

    if result.success then

        m.global.profileId = result.data.profileId
        m.global.profileToken = result.data.profileToken
        m.global.profileIsMain = result.data.loginType = 1 ' Main profile in API

        ' Set theme AFTER setting profileId
        m.global.theme = GetTheme()

        m.top.error = invalid
        m.top.success = true

    else

        m.top.error = result.error
        m.top.success = false

    end if


end sub