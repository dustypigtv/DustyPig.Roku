sub Init()

    m.firstLoad = true

    ' Find controls    
    m.busySpinner = m.top.findNode("busySpinner")
    m.busyLabel = m.top.findNode("busyLabel")
    m.rowList = m.top.findNode("rowList")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.titleLabel = m.top.findNode("titleLabel")
    m.descriptionLabel = m.top.findNode("descriptionLabel")

    
    ' Observers
    m.rowList.observeField("rowItemFocused", "OnFocusedItemChanged")
    m.rowList.observeField("rowItemSelected", "OnItemClicked")
    
    
    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    ' m.errorDialog.observeField("buttonSelected", "OnErrorDialogClosed")
    ' m.errorDialog.observeField("wasClosed", "OnErrorDialogClosed")


    
    ' Home screen task
    m.homeScreenTask = CreateObject("roSGNode", "HomeScreenTask")
    m.homeScreenTask.observeField("success", "OnHomeScreenDataAvailable")

    ' Update timer
    m.updateTimer = CreateObject("roSGNode", "Timer")
    m.updateTimer.repeat = true
    m.updateTimer.duration = 300
    m.updateTimer.ObserveField("fire", "UpdateData")

    ' Observe this
    m.top.observeField("visible", "OnVisibleChanged")
    m.top.observeField("isFocused", "SetFocus")
    m.top.observeField("removeFromContinueWatching", "RemoveFromContinueWatching")

end sub


Sub SetFocus()

    m.rowList.setFocus(true)

End Sub

Sub OnVisibleChanged(event as object)
    
    if event.getData() 
        if m.firstLoad then
            m.firstLoad = false
            UpdateData()
        end if
    end if

End Sub


Sub UpdateData()

    m.homeScreenTask.control = "stop"
    m.homeScreenTask.control = "run"

End Sub



Sub OnHomeScreenDataAvailable()

    m.busySpinner.visible = false
    m.busyLabel.visible = false
    
    if m.homeScreenTask.success then
    
        m.rowList.numRows = m.homeScreenTask.data.getChildCount()
        
        ' Only set the first time. After updates are handled 'in-place'.
        ' Ok not really in-place, there are deep copies and thread rendevous etc,
        ' But for the purpose of this code, just consider it an in-place update
        if m.rowList.content = invalid then m.rowList.content = m.homeScreenTask.data
        OnFocusedItemChanged()

    else if m.homeScreenTask.unauthorized then
        m.switchToLoginAccountView = true
    else
        m.errorDialog.message = [m.homeScreenTask.errMsg]
        m.top.getScene().dialog = m.errorDialog
    end if

    if m.updateTimer.control <> "start" then m.updateTimer.control = "start"

End Sub


' public enum MediaTypes
' {
'     Movie = 1,
'     Series = 2,
'     Episode = 3,
'     Playlist = 4
' }

' Fires when an item in the rowList gets focus
Sub OnFocusedItemChanged()

    try
    
        x = m.rowList.rowItemFocused
        rowContent = m.rowList.content.getChild(x[0])
        selItem = rowContent.getChild(x[1])

        ' Check values before changing to prevent flickering
        backdropUrl = selItem.backdropUrl
        if backdropUrl = "" or backdropUrl = invalid then backdropUrl = selItem.FHDGRIDPOSTERURL
        if m.backdropPoster.uri <> backdropUrl then m.backdropPoster.uri = backdropUrl

        if m.titleLabel.text <> selItem.title then m.titleLabel.text = selItem.title
        
        description = selItem.mediaType = 4 ? "* Playlist *" : selItem.description
        if m.descriptionLabel.text <> description then m.descriptionLabel.text = description
    
    catch e
    
    end try

End Sub


' Fires when an item in the rowList is selected (ok button pressed)
Sub OnItemClicked()

    idx = m.rowList.rowItemSelected
    rowContent = m.rowList.content.getChild(idx[0])
    selItem = rowContent.getChild(idx[1])

    if selItem.mediaType = 1 then m.top.switchToMovieDetailsView = selItem

End sub


Sub RemoveFromContinueWatching(event as object)

    ' public const long ID_CONTINUE_WATCHING = -500;
    section = FindChildNode(m.homeScreenTask.data, "-500")
    if section = invalid then return

    id = event.getData()
    item = FindChildNode(section, id)
    if item = invalid then return

    section.removeChild(item)

    if section.getChildCount() = 0 then m.homeScreenTask.data.removeChild(section)

End Sub