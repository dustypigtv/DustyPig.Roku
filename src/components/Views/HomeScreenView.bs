sub Init()

    print "HomeScreenView:Init"

    m.firstLoad = true

    ' Find controls    
    m.busySpinner = m.top.findNode("busySpinner")
    m.busyLabel = m.top.findNode("busyLabel")
    m.rowList = m.top.findNode("rowList")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.titleLabel = m.top.findNode("titleLabel")
    m.descriptionLabel = m.top.findNode("descriptionLabel")

    
    ' Observers
    m.rowList.observeField("rowItemFocused", "OnRowList_ItemFocused")
    m.rowList.observeField("rowItemSelected", "OnRowList_ItemSelected")
    
    
    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    ' m.errorDialog.observeField("buttonSelected", "OnErrorDialogClosed")
    ' m.errorDialog.observeField("wasClosed", "OnErrorDialogClosed")


    
    ' Home screen task
    m.homeScreenTask = CreateObject("roSGNode", "HomeScreenTask")
    m.homeScreenTask.observeField("success", "OnHomeScreenTask_DataAvailable")



    ' Update timer
    m.updateTimer = CreateObject("roSGNode", "Timer")
    m.updateTimer.repeat = true
    m.updateTimer.duration = 300
    m.updateTimer.ObserveField("fire", "OnUpdateDataTimer_Fired")



    ' Observe this
    m.top.observeField("visible", "OnThis_VisibleChanged")
    m.top.observeField("focusedChild", "OnThis_FocusChanged")
    m.top.observeField("removeFromContinueWatching", "OnThis_RemoveFromContinueWatching")
    m.top.observeField("addToWatchlist", "OnThis_AddToWatchlist")
    m.top.observeField("removeFromWatchlist", "OnThis_RemoveFromWatchlist")

end sub




Sub OnThis_VisibleChanged(event as object)
    
    print "HomeScreenView:OnThis_VisibleChanged"

    if event.getData() 
        if m.firstLoad then
            m.firstLoad = false
            UpdateData()
        end if
    end if

End Sub



Sub OnThis_FocusChanged()

    print "HomeScreenView:OnThis_FocusChanged"

    if m.top.isInFocusChain() then m.rowList.setFocus(true)

End Sub



Sub OnUpdateDataTimer_Fired()

    print "HomeScreenView:OnUpdateDataTimer_Fired"

    UpdateData()

End sub


Sub UpdateData()

    print "HomeScreenView:UpdateData"

    m.homeScreenTask.control = "stop"
    m.homeScreenTask.control = "run"

End Sub




' This needs a root content node
' root.Children are sections, and need the following fields set:
'   id
'   title
' section.children are items, and need the following fields set:
'   id
'   title
'   description
'   FHDGRIDPOSTERURL
'   backdropUrl
'   mediaType
Sub OnHomeScreenTask_DataAvailable()

    print "HomeScreenView:OnHomeScreenTask_DataAvailable"

    m.busySpinner.visible = false
    m.busyLabel.visible = false
    
    if m.homeScreenTask.success then
    
        m.rowList.numRows = m.homeScreenTask.data.getChildCount()
        
        ' Only set the first time. After updates are handled 'in-place'.
        ' Ok not really in-place, there are deep copies and thread rendevous etc,
        ' But for the purpose of this code, just consider it an in-place update
        if m.rowList.content = invalid then m.rowList.content = m.homeScreenTask.data
        OnRowList_ItemFocused()

    else if m.homeScreenTask.unauthorized then
        m.showView_LoginAccount = true
    else
        m.errorDialog.message = [m.homeScreenTask.errMsg]
        m.top.getScene().dialog = m.errorDialog
    end if

    if m.updateTimer.control <> "start" then m.updateTimer.control = "start"

End Sub





' Fires when an item in the rowList gets focus
Sub OnRowList_ItemFocused()

    print "HomeScreenView:OnRowList_ItemFocused"

    try
    
        x = m.rowList.rowItemFocused
        rowContent = m.rowList.content.getChild(x[0])
        selItem = rowContent.getChild(x[1])

        ' Check values before changing to prevent flickering
        backdropUrl = selItem.backdropUrl
        if not StringHasValue(backdropUrl) then backdropUrl = ""
        if m.backdropPoster.uri <> backdropUrl then m.backdropPoster.uri = backdropUrl

        if m.titleLabel.text <> selItem.title then m.titleLabel.text = selItem.title

        ' public enum MediaTypes
        ' {
        '     Movie = 1,
        '     Series = 2,
        '     Episode = 3,
        '     Playlist = 4
        ' }

        description = selItem.mediaType = 4 ? "* Playlist *" : selItem.description
        if m.descriptionLabel.text <> description then m.descriptionLabel.text = description
    
    catch e

        print e

    end try

End Sub


' Fires when an item in the rowList is selected (ok button pressed)
Sub OnRowList_ItemSelected()

    print "HomeScreenView:OnRowList_ItemSelected"

    idx = m.rowList.rowItemSelected
    rowContent = m.rowList.content.getChild(idx[0])
    selItem = rowContent.getChild(idx[1])

    ' public enum MediaTypes
    ' {
    '     Movie = 1,
    '     Series = 2,
    '     Episode = 3,
    '     Playlist = 4
    ' }

    if selItem.mediaType = 1 then m.top.showView_MovieDetails = selItem

End sub



Sub OnThis_RemoveFromContinueWatching(event as object)

    print "HomeScreenView:OnThis_RemoveFromContinueWatching"

    ' public const long ID_CONTINUE_WATCHING = -500
    section = FindChildNode(m.homeScreenTask.data, "-500")
    if section = invalid then return

    id = event.getData()
    item = FindChildNode(section, id)
    if item = invalid then return

    section.removeChild(item)

    if section.getChildCount() = 0 then m.homeScreenTask.data.removeChild(section)

End Sub


' This needs a content node with the following fields set:
' id
' title
' description
' FHDGRIDPOSTERURL
' backdropUrl
' mediaType
Sub OnThis_AddToWatchlist(event as object)

    print "HomeScreenView:OnThis_AddToWatchlist"

    ' public const long ID_CONTINUE_WATCHING = -500
    ' public const long ID_WATCHLIST = -400
    section = FindChildNode(m.homeScreenTask.data, "-400")
    if section = invalid then
       
        ' Add Section
        ' Section is second under Continue Watching 
        continueWatching = FindChildNode(m.homeScreenTask.data, "-500")
        if continueWatching = invalid then idx = 0 else idx = 1
        section = createObject("roSGNode", "ContentNode")
        section.setFields({
            id:"-400",
            title:"Watchlist"
        })
        m.homeScreenTask.data.insertChild(section, idx)

    end if

    newNode = event.getData()
    item = FindChildNode(section, newNode.id)
    if item <> invalid then return

    ' Add Item
    section.appendChild(newNode)
    
End Sub




Sub OnThis_RemoveFromWatchlist(event as object)

    print "HomeScreenView:OnThis_RemoveFromWatchlist"

    ' public const long ID_WATCHLIST = -400
    section = FindChildNode(m.homeScreenTask.data, "-400")
    if section = invalid then return

    id = event.getData()
    item = FindChildNode(section, id)
    if item = invalid then return

    section.removeChild(item)

    if section.getChildCount() = 0 then m.homeScreenTask.data.removeChild(section)

End Sub