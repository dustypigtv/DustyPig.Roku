sub Init()

    m.firstLoad = true

    ' Find controls    
    m.busySpinner = m.top.findNode("busySpinner")
    m.busyLabel = m.top.findNode("busyLabel")
    m.rowList = m.top.findNode("rowList")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.titleLabel = m.top.findNode("titleLabel")
    m.descriptionLabel = m.top.findNode("descriptionLabel")

    
    ' Observers
    m.rowList.observeField("rowItemFocused", "OnRowList_ItemFocused")
    m.rowList.observeField("rowItemSelected", "OnRowList_ItemSelected")
    
    
    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    ' m.errorDialog.observeField("buttonSelected", "OnErrorDialogClosed")
    ' m.errorDialog.observeField("wasClosed", "OnErrorDialogClosed")


    
    ' Home screen task
    m.homeScreenTask = CreateObject("roSGNode", "HomeScreenTask")
    m.homeScreenTask.observeField("success", "OnHomeScreenTask_DataAvailable")



    ' Update timer
    m.updateTimer = CreateObject("roSGNode", "Timer")
    m.updateTimer.repeat = true
    m.updateTimer.duration = m.global.isDev ? 1000 : 300
    m.updateTimer.ObserveField("fire", "OnUpdateDataTimer_Fired")



    ' Observe this
    m.top.observeField("visible", "OnThis_VisibleChanged")
    m.top.observeField("focusedChild", "OnThis_FocusChanged")
    
    
    ' Observer global fields
    m.global.observeField("addToContinueWatching", "OnGlobal_AddToContinueWatching")
    m.global.observeField("removeFromContinueWatching", "OnGlobal_RemoveFromContinueWatching")
    m.global.observeField("addToWatchlist", "OnGlobal_AddToWatchlist")
    m.global.observeField("removeFromWatchlist", "OnGlobal_RemoveFromWatchlist")

end sub



Sub OnThis_VisibleChanged(event as object)
    
    if event.getData() 
        if m.firstLoad then
            m.firstLoad = false
            UpdateData()
        end if
    end if

End Sub



Sub OnThis_FocusChanged()

    if m.top.isInFocusChain() then m.rowList.setFocus(true)

End Sub



Sub OnUpdateDataTimer_Fired()

    UpdateData()

End sub


Sub UpdateData()

    m.homeScreenTask.control = "stop"
    m.homeScreenTask.control = "run"

End Sub




Sub OnHomeScreenTask_DataAvailable()

    m.busySpinner.visible = false
    m.busyLabel.visible = false

    if m.homeScreenTask.success then
    
        m.rowList.numRows = m.homeScreenTask.data.getChildCount()
        if m.rowList.content = invalid then 
            
            m.rowList.content = m.homeScreenTask.data
            OnRowList_ItemFocused()

        else

            currentData = m.rowList.content
            newData =  m.homeScreenTask.data

            dataChanged = false
            if currentData.getChildCount() <> newData.getChildCount() then 
                dataChanged = true
            else
                for i = 0 to currentData.getChildCount() - 1
                    currentSection = currentData.getChild(i)
                    newSection = newData.getChild(i)
                    if currentSection.id <> newSection.id then 
                        dataChanged = true
                        exit for
                    end if
                    for j = 0 to currentSection.getChildCount() - 1
                        currentItem = currentSection.getChild(j)
                        newItem = newSection.getChild(j)
                        if currentItem.id <> newItem.id then 
                            dataChanged = true
                            exit for
                        end if
                    end for
                    if dataChanged then exit for
                end for
            end if

            if dataChanged then

                idx = m.rowList.rowItemFocused
                currentSection = m.rowList.content.getChild(idx[0])
                currentItem = currentSection.getChild(idx[1])

                idx = [0, 0]
                for i = 0 to newData.getChildCount() - 1
                    newSection = newData.getChild(i)
                    if newSection.id = currentSection.id then
                        idx = [i, 0]
                        for j = 0 to newSection.getChildCount() - 1
                            newItem = newSection.getChild(j)
                            if newItem.id = currentItem.id then 
                                idx = [i, j]
                                exit for
                            end if
                        end for
                        exit for
                    end if
                end for
                
                m.rowList.content = m.homeScreenTask.data
                m.rowList.jumpToRowItem = idx

            end if

        end if

    else

        if m.top.visible then 
            m.errorDialog.message = [m.homeScreenTask.error]
            m.top.getScene().dialog = m.errorDialog
        end if
    
    end if

    if m.updateTimer.control <> "start" then m.updateTimer.control = "start"

End Sub










' Fires when an item in the rowList gets focus
Sub OnRowList_ItemFocused()

    try
    
        if not m.top.visible then return

        idx = m.rowList.rowItemFocused
        if idx.count() < 2 then return
        rowContent = m.rowList.content.getChild(idx[0])
        focusedItem = rowContent.getChild(idx[1])

        ' Check values before changing to prevent flickering
        backdropUrl = focusedItem.backdropUrl
        if not StringHasValue(backdropUrl) then backdropUrl = ""
        if m.backdropPoster.uri <> backdropUrl then m.backdropPoster.uri = backdropUrl

        if m.titleLabel.text <> focusedItem.title then m.titleLabel.text = focusedItem.title

        ' public enum MediaTypes
        ' {
        '     Movie = 1,
        '     Series = 2,
        '     Episode = 3,
        '     Playlist = 4
        ' }

        description = focusedItem.mediaType = 4 ? "* Playlist *" : focusedItem.description
        if m.descriptionLabel.text <> description then m.descriptionLabel.text = description

    catch e

        print e

    end try

End Sub


' Fires when an item in the rowList is selected (ok button pressed)
Sub OnRowList_ItemSelected()

    idx = m.rowList.rowItemFocused
    if idx.count() < 2 then return
    rowContent = m.rowList.content.getChild(idx[0])
    selContent = rowContent.getChild(idx[1])
    m.global.currentBasicMedia = selContent

    ' Movie = 1,
    ' Series = 2,
    ' Playlist = 4
    if m.global.currentBasicMedia.mediaType = 1 then m.global.showViewWithId = "movieDetailsView"

End sub


Sub OnGlobal_AddToContinueWatching()

    ' public const long ID_CONTINUE_WATCHING = -500
    continueWatchingSection = FindChildNode(m.rowList.content, "-500")
    if continueWatchingSection = invalid then
        continueWatchingSection = createObject("roSGNode", "ContentNode")
        continueWatchingSection.setFields({
            id: "-500",
            title: "Continue Watching"
        })
        m.rowList.content.insertChild(continueWatchingSection, 0)
    end if
    
    ' Make sure it's at index 0
    found = false
    for i = 0 to continueWatchingSection.getChildCount() - 1
        item = continueWatchingSection.getChild(i)
        if item.id = m.global.addToContinueWatching.id then
            if i > 0 then
                continueWatchingSection.removeChild(item)
                continueWatchingSection.insertChild(item, 0)
            end if
            found = true
            exit for
        end if
    end for
    if not found then continueWatchingSection.insertChild(m.global.addToContinueWatching, 0)

End Sub


Sub OnGlobal_RemoveFromContinueWatching()

    ' public const long ID_CONTINUE_WATCHING = -500
    section = FindChildNode(m.rowList.content, "-500")
    if section <> invalid then
        item = FindChildNode(section, m.global.removeFromContinueWatching)
        if item <> invalid then
            section.removeChild(item)
            if section.getChildCount() = 0 then m.rowList.content.removeChild(section)
        end if
    end if

End Sub


Sub OnGlobal_AddToWatchlist()

    ' public const long ID_CONTINUE_WATCHING = -500
    ' public const long ID_WATCHLIST = -400

    watchlistSection = FindChildNode(m.rowList.content, "-400")
    if watchlistSection = invalid then
        watchlistSection = createObject("roSGNode", "ContentNode")
        watchlistSection.setFields({
            id: "-400",
            title: "Watchlist"
        })
        continueWatchingSection = FindChildNode(m.rowList.content, "-500")
        if continueWatchingSection = invalid then
            m.rowList.content.insertChild(watchlistSection, 0)
        else
            m.rowList.content.insertChild(watchlistSection, 1)
        end if
    end if
    
    item = FindChildNode(watchlistSection, m.global.addToWatchlist.id)
    if item = invalid then watchlistSection.appendChild(m.global.addToWatchlist)
    
End Sub




Sub OnGlobal_RemoveFromWatchlist()

    ' public const long ID_WATCHLIST = -400
    section = FindChildNode(m.rowList.content, "-400")
    if section <> invalid then
        item = FindChildNode(section, m.global.removeFromWatchlist)
        if item <> invalid then
            section.removeChild(item)
            if section.getChildCount() = 0 then m.rowList.content.removeChild(section)
        endif        
    end if

End Sub