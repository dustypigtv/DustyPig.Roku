sub Init()

    ' View References
    m.loginViews = m.top.findNode("loginViews")
    m.emailTextBox = m.loginViews.findNode("emailTextBox")
    m.passwordTextBox = m.loginViews.findNode("passwordTextBox")
    m.loginButton = m.loginViews.findNode("loginButton")
    m.forgotPasswordButton = m.loginViews.findNode("forgotPasswordButton")
    m.signUpButton = m.loginViews.findNode("signUpButton")
    m.busySpinner = m.loginViews.findNode("busySpinner")

    
    ' Button clicks
    m.forgotPasswordButton.observeField("buttonSelected", "OnForgotPasswordButton_Selected")
    m.loginButton.observeField("buttonSelected", "OnLoginButton_Selected")
    m.signUpButton.observeField("buttonSelected", "OnSignUpButton_Selected")


    ' Forgot password success dialog
    m.successDialog = createObject("roSGNode", "StandardMessageDialog")
    m.successDialog.buttons = ["OK"]
    m.successDialog.title = "Success"
    m.successDialog.message = ["Check your email for password reset instructions"]
    m.successDialog.observeField("buttonSelected", "CloseDialog")


    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    m.errorDialog.observeField("buttonSelected", "CloseDialog")



    ' Tasks
    m.loginAccountTask = CreateObject("roSGNode", "LoginAccountTask")
    m.loginAccountTask.observeField("success", "OnLoginAccountTask_Complete")

    m.forgotPasswordTask = CreateObject("roSGNode", "SendPasswordResetEmailTask")
    m.forgotPasswordTask.observeField("success", "OnForgotPassword_Complete")


    ' Focus Tracking
    m.lastFocused = invalid
    m.top.observeField("visible", "OnThis_VisibleChanged")
    m.top.observeField("focusedChild", "OnThis_FocusChanged")
    

    ' Init flow
    if m.global.isDev
    
        ' Test user
        m.emailTextBox.text = "testuser@dustypig.tv"
        m.passwordTextBox.text = "test password"
            
    else

        m.channelStore = CreateObject("roSGNode", "ChannelStore")
        
        ' Set sign-in context for RFI screen
        info = CreateObject("roSGNode", "ContentNode")
        info.addFields({context: "signin"})
        m.channelStore.requestedUserDataInfo = info
        
        m.channelStore.requestedUserData = "email"
        m.channelStore.observeField("userData", "OnChannelStore_DataAvailable")
        m.channelStore.command = "getUserData"
        
    end if

end sub










' ********** FOCUS TRACKING **********

Sub OnThis_FocusChanged(event as object)

    try
        view = event.getData()
        if view.isSameNode(m.top) then
            if m.lastFocused = invalid then m.lastFocused = m.emailTextBox
            m.lastFocused.setFocus(true)
        else
            sv = view
            while sv <> invalid
                if sv.focusedChild.isSameNode(sv) then
                    m.lastFocused = sv.focusedChild
                    exitwhile
                else
                    sv = sv.focusedChild
                end if
            end while
        end if
    catch e
    end try

End Sub


Sub OnThis_VisibleChanged(event as object)

    if event.getData() then ShowControls(true)

End Sub


Sub ShowControls(s as boolean)

    if not m.top.visible then return

    if s then 
   
        m.busySpinner.visible = false
        if m.lastFocused = invalid then m.lastFocused = m.emailTextBox
        m.lastFocused.setFocus(true)
   
    else
   
        if m.lastFocused <> invalid then
            
            ' Drop focus without losing tracking
            lf = m.lastFocused
            m.lastFocused.setFocus(false)
            m.lastFocused = lf
        
        end if
   
        m.busySpinner.visible = true
   
    end if

End Sub










' ********** DIALOG EVENTS **********

Sub OnKeyboardDialog_ButtonSelected(event as object)

    CloseDialog()

    idx = event.getData()
    if idx <> 0 then return

    m.lastFocused.text = event.getRoSGNode().text
    if m.lastFocused.id = m.emailTextBox.id and m.emailTextBox.text = "testuser@dustypig.tv" then m.passwordTextBox.text = "test password"
    if m.emailTextBox.text = "testuser@dustypig.tv" and m.passwordTextBox.text = "test password" then m.loginButton.setFocus(true)
    
End Sub


Sub OnForgotDialog_ButtonSelected(event as object)

    CloseDialog()

    idx = event.getData()
    if idx = 1 then return

    email = event.getRoSGNode().text
    m.passwordTextBox.text = email
    ShowControls(false)

    m.forgotPasswordTask.control = "stop"
    m.forgotPasswordTask.email = email
    m.forgotPasswordTask.control = "run"
    
End Sub

Sub ShowDialog(dlg)

    m.top.getScene().dialog = dlg

End Sub

Sub CloseDialog()

    m.top.getScene().dialog = invalid
    if m.lastFocused <> invalid and not m.lastFocused.hasFocus() then m.lastFocused.setFocus(true)
    
End Sub

Sub ShowErrorDialog(msg as String)

    m.errorDialog.message = [msg]
    ShowDialog(m.errorDialog)

End Sub









' ********** BUTTON CLICKS **********

Sub OnForgotPasswordButton_Selected()

    forgotPasswordDialog = createObject("roSGNode", "StandardKeyboardDialog")
    forgotPasswordDialog.buttons = ["OK", "Cancel"]
    forgotPasswordDialog.title = "Forgot Password"
    forgotPasswordDialog.message = ["Enter your email address"]
    forgotPasswordDialog.text = m.emailTextBox.text
    forgotPasswordDialog.observeField("buttonSelected", "OnForgotDialog_ButtonSelected")
    ShowDialog(forgotPasswordDialog)

End Sub


Sub OnLoginButton_Selected()

    ShowControls(false)

    m.loginAccountTask.email = m.emailTextBox.text
    m.loginAccountTask.password = m.passwordTextBox.text
    m.loginAccountTask.control = "RUN"

End sub


Sub OnSignUpButton_Selected()

    m.global.pushView = CreateObject("roSGNode", "SignupView")

End Sub









' ********** DATA AVAILABLE CALLBACKS **********

Sub OnChannelStore_DataAvailable()

    if m.channelStore.userData <> invalid then
         m.emailTextBox.text = m.channelStore.userData.email
         m.passwordTextBox.setFocus()
    end if

End Sub

Sub OnForgotPassword_Complete()

    ShowControls(true)

    if m.forgotPasswordTask.success then
        ShowDialog(m.successDialog)
    else
        ShowErrorDialog(m.forgotPasswordTask.error)
    end if

End Sub


Sub OnLoginAccountTask_Complete()

    ShowControls(true)

    if m.loginAccountTask.success then

        ' This is only fired when there are profiles
        view = CreateObject("roSGNode", "LoginProfileView")
        view.data = m.loginAccountTask.data
        m.global.pushView(view)

    else

        ShowErrorDialog(m.loginAccountTask.error)

    end if

End Sub









' ********** Key Presses **********

Function onKeyEvent(key as String, press as Boolean) as Boolean

    if not press then return false

    if key = "back" then return true

    if key = "up" then
        if m.passwordTextBox.hasFocus() then
            m.emailTextBox.setFocus(true)
            return true
        else if m.forgotPasswordButton.hasFocus() or m.loginButton.hasFocus() then
            m.passwordTextBox.setFocus(true)
            return true
        else if m.signUpButton.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then
                m.forgotPasswordButton.setFocus(true)
            else
                m.loginButton.setFocus(true)
            end if
            return true
        end if
    end if

    if key = "down" then
        if m.emailTextBox.hasFocus() then
            m.passwordTextBox.setFocus(true)
            return true
        else if m.passwordTextBox.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then 
                m.forgotPasswordButton.setFocus(true)
            else
                m.loginButton.setFocus(true)
            end if
            return true
        else if m.loginButton.hasFocus() or m.forgotPasswordButton.hasFocus() then
            m.signUpButton.setFocus(true)
            return true
        end if
    end if

    if key = "left" then
        if m.loginButton.hasFocus() then
            m.forgotPasswordButton.setFocus(true)
            return true
        end if
    end if

    if key = "right" then
        if m.forgotPasswordButton.hasFocus() then
            m.loginButton.setFocus(true)
            return true
        end if
    end if

    if key = "OK" then 

        if m.emailTextBox.hasFocus() or m.passwordTextBox.hasFocus() then

            keyboardDialog = createObject("roSGNode", "StandardKeyboardDialog")
            keyboardDialog.buttons=["OK", "Cancel"]
            keyboardDialog.observeField("buttonSelected", "OnKeyboardDialog_ButtonSelected")

            if m.emailTextBox.hasFocus() then
                 keyboardDialog.title = "Email Address:"
                 keyboardDialog.keyBoardDomain = "email"
            else
                keyboardDialog.title = "Password:"
                keyboardDialog.keyBoardDomain = "password"
            end if

            keyboardDialog.text = m.lastFocused.text
            keyboardDialog.textEditBox.secureMode = m.passwordTextBox.hasFocus()
            
            ShowDialog(keyboardDialog)
            return true

        end if

    end if

    return false

End Function









' ********** UTILITIES **********

Function GetCredentials() as object

    email = m.emailTextBox.text
    if email = invalid or email = "" then return invalid

    password = m.passwordTextBox.text
    if password = invalid or password = "" then return invalid

    return { "email": email, "password": password }

End Function


