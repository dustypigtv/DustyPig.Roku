sub Init()

    ' Find controls
    m.busySpinner = m.top.findNode("busySpinner")
    m.profileGrid = m.top.findNode("profileGrid")



    ' Init controls
    m.profileGrid.observeField("itemSelected", "OnProfileGrid_ItemSelected")


    ' Login profile task
    m.loginProfileTask = CreateObject("roSGNode", "LoginProfileTask")
    m.loginProfileTask.observeField("success", "OnLoginProfileTask_Complete")


    
    ' Login error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error Signing In To Profile"
    m.errorDialog.observeField("buttonSelected", "CloseDialog")
    

    ' Observe this.visible
    m.top.observeField("data", "OnThis_DataChanged")

end sub


Sub OnThis_DataChanged()

    m.profileGrid.content = m.top.data

End Sub


sub SetBusy(busy as boolean)

    m.profileGrid.visible = not busy
    m.profileGrid.setFocus(not busy)
    m.busySpinner.visible = busy
    
end sub




sub OnProfileGrid_ItemSelected()

    idx = m.profileGrid.itemSelected
    profile = m.top.data.getChild(idx)
    
    if profile.hasPin then
   
        pinDialog = createObject("roSGNode", "StandardPinPadDialog")
        pinDialog.title = "Enter your Pin #:"
        pinDialog.buttons = ["OK"]
        pinDialog.textEditBox.maxTextLength = 4
        pinDialog.observeField("buttonSelected", "OnPinDialog_ButtonSelected")
   
        m.top.getScene().dialog = pinDialog
   
    else
   
        SetBusy(true)
        m.loginProfileTask.id = profile.id
        m.loginProfileTask.pin = invalid
        m.loginProfileTask.control = "RUN"
   
    end if

end sub


sub OnPinDialog_ButtonSelected(event as object)

    CloseDialog()
    
    idx = m.profileGrid.itemSelected
    profile = m.top.data.getChild(idx)    
    
    SetBusy(true)
    
    m.loginProfileTask.id = profile.id
    m.loginProfileTask.pin = event.getRoSGNode().pin
    m.loginProfileTask.control = "RUN"

end sub



Sub OnLoginProfileTask_Complete()

    SetBusy(false)
    
    if m.loginProfileTask.success then

        m.global.loggedOut = false

    else

        m.errorDialog.message = [m.loginProfileTask.error]
        m.top.getScene().dialog = m.errorDialog

    end if

End Sub


sub CloseDialog()

    m.top.getScene().dialog = invalid

end sub