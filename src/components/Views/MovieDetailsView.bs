Sub Init()

    ' Find controls
    m.busySpinner = m.top.findNode("busySpinner")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.mediaInfoView = m.top.findNode("mediaInfoView")
    m.castAndCrewView = m.top.findNode("castAndCrewView")


    ' Tracking focus
    m.lastFocusedControl = invalid
    m.top.observeField("reset", "OnReset")
    m.mediaInfoView.observeField("focusedChild", "OnFocusChanged")
    m.castAndCrewView.observeField("focusedChild", "OnFocusChanged")
    

    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialog_Closed")


    ' Movie Details task
    m.movieDetailsTask = CreateObject("roSGNode", "MovieDetailsTask")
    m.movieDetailsTask.observeField("success", "MovieDetailsTask_Complete")

    ' Observers
    m.top.observeField("data", "OnThis_DataChanged")
    m.top.observeField("visible", "OnThis_VisibleChanged")

end sub







' ********* DATA FIELD **********

Sub OnThis_DataChanged()

    m.backdropPoster.uri = m.top.data.backdropUrl ?? ""

    m.movieDetailsTask.control = "stop"
    m.movieDetailsTask.id = m.top.data.id
    m.movieDetailsTask.control = "run"

End Sub








' ********** FOCUS TRACKING **********

Sub OnThis_VisibleChanged(event as object)

    if event.getData() then RestoreFocus()

End Sub


Sub OnFocusChanged(event as Object)

    node = event.getRoSGNode()
    if node <> invalid then m.lastFocusedControl = node

End Sub













' ********** UPDATE VIEW TRIGGER **********

Sub MovieDetailsTask_Complete()

    if not m.top.visible then return

    if m.movieDetailsTask.success then
    
        dm = m.movieDetailsTask.data
        m.global.currentDetailedMedia = dm

        m.mediaInfoView.visible = true
        m.mediaInfoView.data = dm

        m.castAndCrewView.content = dm.castCrew
        m.castAndCrewView.visible = dm.castCrew.getChildCount() > 0
                            
        RestoreFocus()

        m.busySpinner.visible = false

    else
    
        m.errorDialog.message = [m.movieDetailsTask.error]
        m.top.getScene().dialog = m.errorDialog
    
    end if

End Sub








' ********** ERROR DIALOG **********

Sub OnErrorDialog_Closed()

    m.errorDialog.close = true
    m.global.popCurrentView = true

End Sub







' ********** KEY PRESSES **********

Function onKeyEvent(key as String, press as Boolean) as Boolean

    if not press then return false

    if key = "up" then

        if m.castAndCrewView.isInFocusChain() then
            m.mediaInfoView.setFocus(true)
            return true
        end if

    end if

    if key = "down" then

        if m.castAndCrewView.visible and not m.castAndCrewView.isInFocusChain() then
            m.castAndCrewView.setFocus(true)
            return true
        end if

    end if

    return false

End Function






' ********** UTILITIES **********

Sub RestoreFocus()

    if m.lastFocusedControl = invalid or m.lastFocusedControl.visible = false then
        m.mediaInfoView.setFocus(true)
    else
        m.lastFocusedControl.setFocus(true)
    end if

End Sub