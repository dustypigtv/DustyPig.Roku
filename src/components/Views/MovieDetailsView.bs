Sub Init()

    ' Find controls
    m.busySpinner = m.top.findNode("busySpinner")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.mediaInfoView = m.top.findNode("mediaInfoView")
    m.castAndCrewView = m.top.findNode("castAndCrewView")


    ' Tracking focus
    m.lastFocusedControl = invalid
    m.top.observeField("reset", "OnResetFocus")
    m.mediaInfoView.observeField("focusedChild", "OnFocusChanged")
    m.castAndCrewView.observeField("focusedChild", "OnFocusChanged")
    

    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialog_Closed")


    ' Movie Details task
    m.movieDetailsTask = CreateObject("roSGNode", "MovieDetailsTask")
    m.movieDetailsTask.observeField("state", "SetupView")


    ' Observers
    m.top.observeField("visible", "SetupView")


    m.global.observeField("currentBasicMedia", "OnGlobal_CurrentBasicMediaChanged")

end sub





' ********** FOCUS TRACKING **********

Sub OnResetFocus()

    m.lastFocusedControl = invalid
    m.mediaInfoView.resetFocusTracking = true

End Sub


Sub OnFocusChanged(event as Object)

    node = event.getRoSGNode()
    if node <> invalid then m.lastFocusedControl = node

End Sub







' ********** DATA UPDATE TRIGGER **********

Sub OnGlobal_CurrentBasicMediaChanged()
    
    bm = m.global.currentBasicMedia
    if bm = invalid then return
    if bm.contentType <> 1 then return

    Setbackdrop()
    
    m.movieDetailsTask.control = "stop"
    m.movieDetailsTask.id = bm.id
    m.movieDetailsTask.control = "run"

End Sub






' ********** UPDATE VIEW TRIGGER **********

Sub SetupView()

    if not m.top.visible then return

    SetBackdrop()

    if m.movieDetailsTask.state = "run" then
        
        m.mediaInfoView.visible = false
        m.castAndCrewView.visible = false
        m.busySpinner.visible = true
        
    else

        if m.movieDetailsTask.success then
        
            dm = m.movieDetailsTask.data
            m.global.currentDetailedMedia = dm

            m.castAndCrewView.content = dm.castCrew
            m.castAndCrewView.visible = dm.castCrew.getChildCount() > 0
            
            m.mediaInfoView.visible = true
                        
            if m.lastFocusedControl = invalid or m.lastFocusedControl.visible = false then
                m.mediaInfoView.setFocus(true)
            else
                m.lastFocusedControl.setFocus()
            end if

            m.busySpinner.visible = false

        else
        
            m.errorDialog.message = [m.movieDetailsTask.error]
            m.top.getScene().dialog = m.errorDialog
        
        end if

    end if

End Sub








' ********** ERROR DIALOG **********

Sub OnErrorDialog_Closed()

    m.top.getScene().dialog = invalid

End Sub







' ********** KEY PRESSES **********

Function onKeyEvent(key as String, press as Boolean) as Boolean

    if not press then return false

    if key = "up" then

        if m.castAndCrewView.isInFocusChain() then
            m.mediaInfoView.setFocus(true)
            return true
        end if

    end if

    if key = "down" then

        if m.castAndCrewView.visible and not m.castAndCrewView.isInFocusChain() then
            m.castAndCrewView.setFocus(true)
            return true
        end if

    end if

    return false

End Function






' ********** UTILITIES **********


Sub SetBackdrop()

    bm = m.global.currentBasicMedia
    if bm = invalid then return
    if bm.contentType <> 1 then return
    m.backdropPoster.uri = bm.backdropUrl ?? ""
    
End Sub
