Sub Init()

    ' Find controls    
    m.busySpinner = m.top.findNode("busySpinner")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.titleLabel = m.top.findNode("titleLabel")
    m.yearLabel = m.top.findNode("yearLabel")
    m.ratedLabel = m.top.findNode("ratedLabel")
    m.lengthLabel = m.top.findNode("lengthLabel")
    m.remainingLabel = m.top.findNode("remainingLabel")
    m.playButton = m.top.findNode("playButton")
    m.requestButton = m.top.findNode("requestButton")
    m.resumeButton = m.top.findNode("resumeButton")
    m.markWatchedButton = m.top.findNode("markWatchedButton")
    m.watchlistButton = m.top.findNode("watchlistButton")
    m.addToPlaylistButton = m.top.findNode("addToPlaylistButton")
    m.descriptionLabel = m.top.findNode("descriptionLabel")
    m.castCrewRowList = m.top.findNode("castCrewRowList")
    m.selectedPersonLabel = m.top.findNode("selectedPersonLabel")

    m.playbackButtonLayoutGroup = m.top.findNode("playbackButtonLayoutGroup")
    m.otherActionButtonLayoutGroup = m.top.findNode("otherActionButtonLayoutGroup")

    ' Tracking focus
    m.lastFocusedControl = invalid

    m.lastPlaybackButtonFocused = m.playButton
    m.playButton.observeField("focusedChild", "OnPlaybackButtons_FocusChanged")
    m.requestButton.observeField("focusedChild", "OnPlaybackButtons_FocusChanged")
    m.resumeButton.observeField("focusedChild", "OnPlaybackButtons_FocusChanged")
    m.markWatchedButton.observeField("focusedChild", "OnPlaybackButtons_FocusChanged")

    m.lastOtherActionButtonFocused = m.watchlistButton
    m.watchlistButton.observeField("focusedChild", "OnOtherActionButtons_FocusChanged")
    m.addToPlaylistButton.observeField("focusedChild", "OnOtherActionButtons_FocusChanged")

    m.castCrewRowList.observeField("focusedChild", "OnCastCrewList_FocusChanged")
    m.castCrewRowList.observeField("rowItemFocused", "OnCastCrewList_ItemFocused")


    ' Button clicks
    m.playButton.observeField("buttonSelected", "OnPlayButton_Selected")
    m.requestButton.observeField("buttonSelected", "OnRequestButton_Selected")
    m.resumeButton.observeField("buttonSelected", "OnResumeButton_Selected")
    m.markWatchedButton.observeField("buttonSelected", "OnMarkWatchedButton_Selected")
    m.watchlistButton.observeField("buttonSelected", "OnWatchlistButton_Selected")
    m.addToPlaylistButton.observeField("buttonSelected", "OnAddToPlaylistButton_Selected")



    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialog_Closed")


    ' Movie Details task
    m.movieDetailsTask = CreateObject("roSGNode", "MovieDetailsTask")
    m.movieDetailsTask.observeField("success", "OnMovieDetailsTask_DataAvailable")


    ' Mark Watched task
    m.markMovieWatchedTask = CreateObject("roSGNode", "MarkMovieWatchedTask")

    ' Toggle Watchlist task
    m.toggleWatchlistTask = CreateObject("roSGNode", "ToggleWatchlistTask")


    ' Observers
    m.top.observeField("visible", "OnThis_VisibleChanged")
    m.top.observeField("beforeShowAfterPush", "OnThis_BeforeShowAfterPush")

    ' Global observers
    m.global.observeField("currentBasicMedia", "OnGlobal_CurrentBasicMediaChanged")
    m.global.observeField("addToContinueWatching", "OnGlobal_AddToContinueWatching")
    m.global.observeField("removeFromContinueWatching", "OnGlobal_RemoveFromContinueWatching")
    m.global.observeField("playbackPosition", "OnGlobal_PlaybackPositionChanged")

end sub


Sub OnThis_BeforeShowAfterPush(event as object)

    if event.getData() then m.lastFocusedControl = invalid

End Sub


Sub OnThis_VisibleChanged(event as object)

    if event.getData() then RestoreFocus()

End Sub



Sub RestoreFocus()

    if not m.top.visible then return

    if m.lastFocusedControl = invalid then
        SetFocusFromRuleList()
    else
        if m.lastFocusedControl.visible then
            m.lastFocusedControl.setFocus(true)
        else
            SetFocusFromRuleList()
        end if
    end if

End Sub

Sub SetFocusFromRuleList()

    if not m.top.visible then return

    if m.playButton.visible then
        m.playButton.setFocus(true)
    else if m.requestButton.visible then
        m.requestButton.setFocus(true)
    else if m.castCrewRowList.visible then
        m.castCrewRowList.setFocus(true)
    end if

End sub




Sub OnPlaybackButtons_FocusChanged(event as object)

    if m.playbackButtonLayoutGroup.isInFocusChain() then
        m.lastPlaybackButtonFocused = event.getRoSGNode()
        m.lastFocusedControl = m.lastPlaybackButtonFocused
    end if

End Sub

Sub OnOtherActionButtons_FocusChanged(event as object)

    if m.otherActionButtonLayoutGroup.isInFocusChain() then
        m.lastOtherActionButtonFocused = event.getRoSGNode()
        m.lastFocusedControl = m.lastOtherActionButtonFocused
    end if

End Sub


Sub OnErrorDialog_Closed()

    m.top.getScene().dialog = invalid
    RestoreFocus()

End Sub


Sub OnCastCrewList_ItemFocused()

    idx = m.castCrewRowList.rowItemFocused
    rowContent = m.castCrewRowList.content.getChild(idx[0])
    selItem = rowContent.getChild(idx[1])
    m.selectedPersonLabel.text = selItem.title

End sub


Sub OnCastCrewList_FocusChanged()

    if m.castCrewRowList.hasFocus() then 
        m.lastFocusedControl = m.castCrewRowList    
    else
        m.selectedPersonLabel.text = invalid 
    end if

End Sub


' Fires when the BaseScene sets the Content node
Sub OnGlobal_CurrentBasicMediaChanged()

    if m.global.currentBasicMedia = invalid then return
    if m.global.currentBasicMedia.mediaType <> 1 then return ' Movie
    
    m.movieDetailsTask.control = "stop"
    
    
    ' Reset
    m.titleLabel.text = invalid
    m.yearLabel.text = invalid
    m.ratedLabel.text = invalid
    m.lengthLabel.text = invalid
    m.remainingLabel.text = invalid
    m.playButton.visible = false
    m.requestButton.visible = false
    m.resumeButton.visible = false
    m.markWatchedButton.visible = false
    m.watchlistButton.visible = false
    m.addToPlaylistButton.visible = false
    m.descriptionLabel.text = invalid
    m.castCrewRowList.content = invalid
    m.selectedPersonLabel.text = invalid
    m.busySpinner.visible = true

    m.lastOtherActionButtonFocused = m.watchlistButton
    
    m.movieDetailsTask.id = m.global.currentBasicMedia.id
    m.movieDetailsTask.control = "run"

    m.backdropPoster.uri = m.global.currentBasicMedia.backdropUrl ?? ""
    
End Sub


Sub OnMovieDetailsTask_DataAvailable()

    m.busySpinner.visible = false

    if m.movieDetailsTask.success then
 
        m.global.currentDetailedMedia = m.movieDetailsTask.data
        m.global.playbackPosition = m.movieDetailsTask.playbackPosition
        
        m.titleLabel.text = m.global.currentDetailedMedia.title
        m.yearLabel.text = m.global.currentDetailedMedia.secondaryTitle
        m.ratedLabel.text = "Rated: " + m.global.currentDetailedMedia.rating

        if m.global.currentDetailedMedia.length > 0 then m.lengthLabel.text = GetDurationString(m.global.currentDetailedMedia.length)

        if m.global.currentDetailedMedia.canPlay then
           
            m.playButton.text = "Play"

            if m.global.currentDetailedMedia.length > 0 and m.global.playbackPosition > 0 then 
                m.remainingLabel.text = GetDurationString(m.global.currentDetailedMedia.length - m.global.playbackPosition) + " remaining"
                m.resumeButton.visible = true
                m.lastFocusedControl = m.resumeButton
                m.markWatchedButton.visible = true
            end if

            m.playButton.visible = true

            m.watchlistButton.icon = m.global.currentDetailedMedia.inWatchList ? "pkg:/images/check.png" : "pkg:/images/add.png"
            m.watchlistButton.visible = true
            
            m.addToPlaylistButton.visible = true
        
        else

            m.requestButton.visible = true

        end if
        
        if not m.resumeButton.visible then 
            if m.playButton.visible then 
                m.lastFocusedControl = m.playButton
            else if m.requestButton.visible then 
                m.lastFocusedControl = m.requestButton
            end if
        end if

        m.descriptionLabel.text = m.global.currentDetailedMedia.description
        m.castCrewRowList.content = m.global.currentDetailedMedia.castCrew
        
        RestoreFocus()

    else
    
        m.errorDialog.message = [m.movieDetailsTask.error]
        m.top.getScene().dialog = m.errorDialog
    
    end if

End Sub


Sub OnPlayButton_Selected()

    StartPlayback(false)

End Sub

Sub OnResumeButton_Selected()

    StartPlayback(true)

End Sub


Sub StartPlayback(resume as boolean)

    m.global.currentDetailedMedia.playStart = resume ? m.global.playbackPostion : 0
    m.global.startVideoPlayback = true
    
End Sub




Sub OnRequestButton_Selected()

End Sub







Sub OnMarkWatchedButton_Selected()

    m.markMovieWatchedTask.id = m.global.currentDetailedMedia.id
    m.markMovieWatchedTask.control = "run"

    m.global.playbackPosition = 0
    m.global.removeFromContinueWatching = m.global.currentDetailedMedia.id

End Sub




Sub OnWatchlistButton_Selected()

    m.toggleWatchlistTask.control = "stop"
    
    add = not m.global.currentDetailedMedia.inWatchList ?? false

    m.toggleWatchlistTask.add = add
    m.toggleWatchlistTask.id = m.global.currentDetailedMedia.id
    m.toggleWatchlistTask.control = "run"

    m.watchlistButton.icon = add ? "pkg:/images/check.png" : "pkg:/images/add.png"
    if add then 
        m.global.currentDetailedMedia.inWatchList = true
        m.global.addToWatchlist = BuildBasicMediaFromDetailed(m.global.currentDetailedMedia)
    else 
        m.global.currentDetailedMedia.inWatchList = false
        m.global.removeFromWatchlist = m.global.currentDetailedMedia.id
    end if
    
End Sub



Sub OnAddToPlaylistButton_Selected()

    ' m.top.relay_switchToAddToPlaylist = {
    '     id:m.global.currentMedia.id,
    '     mediaType:m.global.currentMedia.mediaType
    ' }

End Sub



Sub OnGlobal_AddToContinueWatching()

    if m.movieDetailsTask.id <> m.global.currentDetailedMedia.id then return

    m.remainingLabel.text = GetDurationString(m.global.currentDetailedMedia.length - m.global.playbackPosition) + " remaining"
    m.resumeButton.visible = true
    m.markWatchedButton.visible = true

End sub


Sub OnGlobal_RemoveFromContinueWatching()

    if m.movieDetailsTask.id <> m.global.currentDetailedMedia.id then return

    if m.playbackButtonLayoutGroup.isInFocusChain() then m.playButton.setFocus(true)
    m.markWatchedButton.visible = false
    m.resumeButton.visible = false
    m.remainingLabel.text = invalid

End sub

Sub OnGlobal_PlaybackPositionChanged()

    if m.movieDetailsTask.id <> m.global.currentDetailedMedia.id then return

    m.remainingLabel.text = GetDurationString(m.global.currentDetailedMedia.length - m.global.playbackPosition) + " remaining"
    m.resumeButton.visible = true
    m.markWatchedButton.visible = true

End Sub


Function onKeyEvent(key as String, press as Boolean) as Boolean

    if not press then return false

    if key = "right" then

        if m.playButton.hasFocus() and m.resumeButton.visible then 
            
            m.resumeButton.setFocus(true)
            return true
        
        end if

        if m.resumeButton.hasFocus() then

            m.markWatchedButton.setFocus(true)
            return true

        end if

        if m.watchlistButton.hasFocus() then

            m.addToPlaylistButton.setFocus(true)
            return true

        end if

    end if




    if key = "left" then

        if m.markWatchedButton.hasFocus() then

            m.resumeButton.setFocus(true)
            return true

        end if

        if m.resumeButton.hasFocus() then 

            m.playButton.setFocus(true)
            return true
        
        end if

        if m.addToPlaylistButton.hasFocus() then

            m.watchlistButton.setFocus(true)
            return true

        end if

    end if


    if key = "up" then

        if m.otherActionButtonLayoutGroup.isInFocusChain() then

            m.lastPlaybackButtonFocused.setFocus(true)
            return true

        end if

        if m.castCrewRowList.isInFocusChain() then

            m.lastOtherActionButtonFocused.setFocus(true)
            return true

        end if

    end if



    if key = "down" then

        if m.playbackButtonLayoutGroup.isInFocusChain() then

            m.lastOtherActionButtonFocused.setFocus(true)
            return true

        end if

        if m.otherActionButtonLayoutGroup.isInFocusChain() then

            m.castCrewRowList.setFocus(true)
            return true

        end if

    end if


    
    return false

End Function
