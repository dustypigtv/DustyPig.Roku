Sub Init()

    ' Find controls    
    m.busySpinner = m.top.findNode("busySpinner")
    m.backdropPoster = m.top.findNode("backdropPoster")
    m.titleLabel = m.top.findNode("titleLabel")
    m.yearLabel = m.top.findNode("yearLabel")
    m.ratedLabel = m.top.findNode("ratedLabel")
    m.lengthLabel = m.top.findNode("lengthLabel")
    m.remainingLabel = m.top.findNode("remainingLabel")
    m.playButton = m.top.findNode("playButton")
    m.requestButton = m.top.findNode("requestButton")
    m.resumeButton = m.top.findNode("resumeButton")
    m.markWatchedButton = m.top.findNode("markWatchedButton")
    m.watchlistButton = m.top.findNode("watchlistButton")
    m.playlistButton = m.top.findNode("playlistButton")
    m.descriptionLabel = m.top.findNode("descriptionLabel")
    m.castCrewRowList = m.top.findNode("castCrewRowList")
    m.selectedPersonLabel = m.top.findNode("selectedPersonLabel")

    m.playbackButtonLayoutGroup = m.top.findNode("playbackButtonLayoutGroup")
    m.otherActionLayoutGroup = m.top.findNode("otherActionLayoutGroup")

    ' Tracking focus
    m.lastPlaybackButtonFocused = m.playButton
    m.playButton.observeField("focusedChild", "OnPlaybackButtonsFocusedChildChanged")
    m.requestButton.observeField("focusedChild", "OnPlaybackButtonsFocusedChildChanged")
    m.resumeButton.observeField("focusedChild", "OnPlaybackButtonsFocusedChildChanged")
    m.markWatchedButton.observeField("focusedChild", "OnPlaybackButtonsFocusedChildChanged")

    m.lastOtherActionButtonFocused = m.watchlistButton
    m.watchlistButton.observeField("focusedChild", "OnOtherActionsButtonsFocusedChildChanged")
    m.playlistButton.observeField("focusedChild", "OnOtherActionsButtonsFocusedChildChanged")

    m.castCrewRowList.observeField("focusedChild", "HidePersonName")
    m.castCrewRowList.observeField("rowItemFocused", "OnFocusedPersonChanged")


    ' Button clicks
    m.playButton.observeField("buttonSelected", "OnPlayClicked")
    m.requestButton.observeField("buttonSelected", "OnRequestClicked")
    m.resumeButton.observeField("buttonSelected", "OnResumeClicked")
    m.markWatchedButton.observeField("buttonSelected", "OnMarkWatchClicked")
    m.watchlistButton.observeField("buttonSelected", "OnWatchlistClicked")
    m.playlistButton.observeField("buttonSelected", "OnAddToPlaylistClicked")



    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialogClosed")


    ' Movie Details task
    m.movieDetailsTask = CreateObject("roSGNode", "MovieDetailsTask")
    m.movieDetailsTask.observeField("data", "OnMovieDataAvailable")


    ' Mark Watched task
    m.markMovieWatchedTask = CreateObject("roSGNode", "MarkMovieWatchedTask")
    m.markMovieWatchedTask.observeField("success", "OnMarkMovieWatchedComplete")


    ' Timer - Create AFTER creating the movieDetailsTask
    m.timer = CreateObject("roSGNode", "Timer")
    m.timer.repeat = true
    m.timer.duration = 0.250
    m.timer.ObserveField("fire","TimerFired")
    m.timer.control = "start"
  

    ' Observers
    m.top.observeField("visible", "OnVisibilityChanged")
    m.top.observeField("content", "OnContentSet")

end sub


Sub OnVisibilityChanged(event as object)

    if not event.getData() then m.movieDetailsTask.control = "stop"

End Sub


Sub OnPlaybackButtonsFocusedChildChanged(event as object)

    m.lastPlaybackButtonFocused = event.getRoSGNode()

End Sub

Sub OnOtherActionsButtonsFocusedChildChanged(event as object)

    m.lastOtherActionButtonFocused = event.getRoSGNode()

End Sub


Sub OnErrorDialogClosed()

    m.top.getScene().dialog = invalid

End Sub


Sub TimerFired()

    m.busySpinner.visible = m.top.visible and m.movieDetailsTask.state = "run"

End sub



Sub OnFocusedPersonChanged()

    idx = m.castCrewRowList.rowItemFocused
    rowContent = m.castCrewRowList.content.getChild(idx[0])
    selItem = rowContent.getChild(idx[1])
    m.selectedPersonLabel.text = selItem.title

End sub


Sub HidePersonName()

    if not m.castCrewRowList.hasFocus() then m.selectedPersonLabel.text = invalid 

End Sub


' Fires when the BaseScene sets the Content node
Sub OnContentSet()

    m.movieDetailsTask.control = "stop"
    

    ' Reset
    m.titleLabel.text = invalid
    m.yearLabel.text = invalid
    m.ratedLabel.text = invalid
    m.lengthLabel.text = invalid
    m.remainingLabel.text = invalid
    m.playButton.visible = false
    m.requestButton.visible = false
    m.resumeButton.visible = false
    m.markWatchedButton.visible = false
    m.watchlistButton.visible = false
    m.playButton.visible = false
    m.descriptionLabel.text = invalid
    m.castCrewRowList.content = invalid
    m.selectedPersonLabel.text = invalid

    m.lastOtherActionButtonFocused = m.watchlistButton


    ' Load
    content = m.top.content
    
    m.movieDetailsTask.id = content.id
    m.movieDetailsTask.control = "run"

    m.backdropPoster.uri = content.backdropUrl
    
End Sub


Sub OnMovieDataAvailable()

    if not m.top.visible then return

    result = m.movieDetailsTask.data
    if result.success then
        
        data = result.data
        m.titleLabel.text = data.title

        try
            date = CreateObject("roDateTime")
            date.FromISO8601String(data.date)
            m.yearLabel.text = str(date.getYear())
        catch e
            m.yearLabel.text = "1900"
        end try

        ' From API
        ' public enum MovieRatings
        ' {
        '     None = 0,
        '     G = 1,
        '     PG = 2,
        '     PG_13 = 3,
        '     R = 4,
        '     NC_17 = 5,
        '     Unrated = 6
        ' }        
        if data.rated = 1 then 
            m.ratedLabel.text = "Rated G"
        else if data.rated = 2 then 
            m.ratedLabel.text = "Rated PG"
        else if data.rated = 3 then 
            m.ratedLabel.text = "Rated PG-13"
        else if data.rated = 4 then 
            m.ratedLabel.text = "Rated R"
        else if data.rated = 5 then 
            m.ratedLabel.text = "Rated NC-17"
        else if data.rated = 6 then 
            m.ratedLabel.text = "Unrated"
        else  
            m.ratedLabel.text = "Not Rated"
        end if

        if data.length <> invalid and data.length > 0 then m.lengthLabel.text = GetDurationString(data.length)

        if data.canPlay then
           
            m.playButton.text = "Play"

            if data.length <> invalid and data.length > 0 and data.played <> invalid and data.played > 60 then 
                m.remainingLabel.text = GetDurationString(data.length - data.played) + " remaining"
                m.resumeButton.visible = true
                m.resumeButton.setFocus(true)
                m.markWatchedButton.visible = true
            end if

            m.playButton.visible = true
            m.watchlistButton.visible = true
            m.playlistButton.visible = true
        
        else

            m.requestButton.visible = true

        end if
        
        if not m.resumeButton.visible then 
            if m.playButton.visible then 
                m.playButton.setFocus(true)
            else if m.requestButton.visible then 
                m.requestButton.setFocus(true)
            end if
        end if


        m.descriptionLabel.text = data.description

        m.castCrewRowList.content = m.movieDetailsTask.castCrew
        

        
    else if result.statusCode = 401 then
    
        m.top.switchToLoginAccountView = true
    else
    
        m.errorDialog.message = [result.error]
        m.top.getScene().dialog = m.errorDialog
    
    end if

End Sub


Sub OnPlayClicked()

End Sub



Sub OnRequestClicked()

End Sub



Sub OnResumeClicked()

End Sub



Sub OnMarkWatchClicked()

    m.markMovieWatchedTask.id = m.top.content.id
    m.markMovieWatchedTask.control = "run"
    m.playButton.setFocus(true)
    m.markWatchedButton.visible = false
    m.resumeButton.visible = false
    m.remainingLabel.text = "invalid"

End Sub

Sub OnMarkMovieWatchedComplete()

    if m.markMovieWatchedTask.success then m.top.updateHomeScreen = true
End sub


Sub OnWatchlistClicked()

End Sub



Sub OnAddToPlaylistClicked()

End Sub



Function onKeyEvent(key as String, press as Boolean) as Boolean

    if not press then return false

    if key = "back" then 

        m.top.hideView = true
        return true

    end if



    if key = "right" then

        if m.playButton.hasFocus() and m.resumeButton.visible then 
            
            m.resumeButton.setFocus(true)
            return true
        
        end if

        if m.resumeButton.hasFocus() then

            m.markWatchedButton.setFocus(true)
            return true

        end if

        if m.watchlistButton.hasFocus() then

            m.playlistButton.setFocus(true)
            return true

        end if

    end if




    if key = "left" then

        if m.markWatchedButton.hasFocus() then

            m.resumeButton.setFocus(true)
            return true

        end if

        if m.resumeButton.hasFocus() then 

            m.playButton.setFocus(true)
            return true
        
        end if

        if m.playlistButton.hasFocus() then

            m.watchlistButton.setFocus(true)
            return true

        end if

    end if


    if key = "up" then

        if m.otherActionLayoutGroup.isInFocusChain() then

            m.lastPlaybackButtonFocused.setFocus(true)
            return true

        end if

        if m.castCrewRowList.isInFocusChain() then

            m.lastOtherActionButtonFocused.setFocus(true)
            return true

        end if

    end if



    if key = "down" then

        if m.playbackButtonLayoutGroup.isInFocusChain() then

            m.lastOtherActionButtonFocused.setFocus(true)
            return true

        end if

        if m.otherActionLayoutGroup.isInFocusChain() then

            m.castCrewRowList.setFocus(true)
            return true

        end if

    end if


    
    return false

End Function
