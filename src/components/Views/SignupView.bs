sub Init()
    
    print "SignUpView:Init"

    ' Find controls    
    m.headerLabel = m.top.findNode("headerLabel")
    m.nameTextBox = m.top.findNode("nameTextBox")
    m.emailTextBox = m.top.findNode("emailTextBox")
    m.passwordTextBox = m.top.findNode("passwordTextBox")
    m.loginAccountButton = m.top.findNode("loginAccountButton")
    m.signUpButton = m.top.findNode("signUpButton")
    m.busySpinner = m.top.findNode("busySpinner")


    ' Init controls
    m.headerLabel.color = m.global.defaultTheme.textOnBackground
    m.headerLabel.visible = true

    m.busySpinner.poster.blendColor = m.global.defaultTheme.primary
    
    m.loginAccountButton.focusBitmapUri = m.global.defaultTheme.primaryBitmap
    m.loginAccountButton.observeField("buttonSelected", "OnLoginAccountButton_Selected")

    m.signUpButton.focusBitmapUri = m.global.defaultTheme.primaryBitmap
    m.signUpButton.observeField("buttonSelected", "OnSignUpButton_Selected")



    
    ' Build keyboard dialog
    m.keyboardDialog = createObject("roSGNode", "StandardKeyboardDialog")
    m.keyboardDialog.buttons=["Cancel","OK"]
    m.keyboardDialog.observeField("wasClosed", "OnKeyboardDialog_Closed")
    m.keyboardDialog.observeField("buttonSelected", "OnKeyboardDialog_TextEntered")




    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Sign up error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialog_Closed")
    m.errorDialog.observeField("wasClosed", "OnErrorDialog_Closed")


    ' Signup task
    m.signUpTask = CreateObject("roSGNode", "SignUpTask")
    m.signUpTask.observeField("data", "OnSignUpTask_DataAvailable")


    'variables
    m.lastSelected = invalid
    m.alreadyAskedChannelStore = false


    ' Observe this.visible
    m.top.observeField("visible", "OnThis_VisibleChanged")

end sub


' Fires when this view's visibility changes
Sub OnThis_VisibleChanged(event as object)

    print "SignUpView:OnThis_VisibleChanged"

    m.busySpinner.visible = false
    if not event.getData() then return

    m.lastSelected = m.nameTextBox
    ShowControls(true)

    ' Test user
    if m.global.isDev

        m.nameTextBox.text = "New Test User"
        m.emailTextBox.text = "newtestuser@dustypig.tv"
        m.passwordTextBox.text = "test password"

    else if m.alreadyAskedChannelStore = false

        m.alreadyAskedChannelStore = true
        m.channelStore = CreateObject("roSGNode", "ChannelStore")
        
        ' Set sign-in context for RFI screen
        info = CreateObject("roSGNode", "ContentNode")
        info.addFields({context: "signup"})
        m.channelStore.requestedUserDataInfo = info
        
        m.channelStore.requestedUserData = "firstName, email"
        m.channelStore.observeField("userData", "OnChannelStore_DataAvailable")
        m.channelStore.command = "getUserData"

    end if

End Sub


sub OnChannelStore_DataAvailable()

    print "SignUpView:OnChannelStore_DataAvailable"

    if m.channelStore.userData <> invalid then
        m.nameTextBox.text = m.channelStore.userData.firstName
        m.emailTextBox.text = m.channelStore.userData.email
        FocusOnTB(m.passwordTextBox)
    end if

end sub


Sub ShowControls(s as boolean)

    print "SignUpView:ShowControls", s

    if not s then

        if m.nameTextBox.hasFocus() then m.lastSelected = m.nameTextBox
        if m.emailTextBox.hasFocus() then m.lastSelected = m.emailTextBox
        if m.passwordTextBox.hasFocus() then m.lastSelected = m.passwordTextBox

    end if

    m.headerLabel.visible = s
    m.nameTextBox.visible = s
    m.emailTextBox.visible = s
    m.passwordTextBox.visible = s
    m.loginAccountButton.visible = s
    m.signUpButton.visible = s

    if s then FocusOnTB(m.lastSelected)

End Sub


Sub FocusOnTB(tb as object)

    print "SignUpView:FocusOnTB"

    m.nameTextBox.textColor = "#808080"
    m.emailTextBox.textColor = "#808080"
    m.passwordTextBox.textColor = "#808080"

    if tb = invalid then tb = m.nameTextBox

    tb.setFocus(true)
    tb.textColor = m.global.defaultTheme.primary

End Sub


Sub FocusOnButton(btn as object)

    print "SignUpView:FocusOnButton"

    m.emailTextBox.textColor = "#808080"
    m.passwordTextBox.textColor = "#808080"

    if btn = invalid then btn = m.signUpButton

    btn.setFocus(true)

End Sub


Sub OnKeyboardDialog_Closed()

    print "SignUpView:OnKeyboardDialog_Closed"

    ShowControls(true)

End Sub


Sub OnKeyboardDialog_TextEntered(event as object)

    print "SignUpView:OnKeyboardDialog_TextEntered"

    m.top.getScene().dialog = invalid
    idx = event.getData()
    selectSignup = false
    if idx = 1 then 
        m.lastSelected.text = m.keyboardDialog.text
        if m.lastSelected.id = m.emailTextBox.id and m.emailTextBox.text = "newtestuser@dustypig.tv" then 
            m.nameTextBox.text = "New Test User"
            m.passwordTextBox.text = "test password"
            selectSignup = true
        end if
    end if
    ShowControls(true)
    if selectSignup then FocusOnButton(m.signUpButton)

End Sub

Sub OnErrorDialog_Closed()

    print "SignUpView:OnErrorDialog_Closed"

    m.top.getScene().dialog = invalid
    ShowControls(true)

End Sub


Sub OnLoginAccountButton_Selected()

    print "SignUpView:OnLoginAccountButton_Selected"

    m.top.showViewWithId = "loginAccountView"

End sub


Sub OnSignUpButton_Selected()

    print "SignUpView:OnSignUpButton_Selected"

    m.signUpButton.setFocus(false)
    m.busySpinner.visible = true

    credentials = GetCredentials()
    m.signUpTask.displayName = credentials.displayName
    m.signUpTask.email = credentials.email
    m.signUpTask.password = credentials.password
    m.signUpTask.control = "RUN"

End Sub

Function GetCredentials() as object

    print "SignUpView:GetCredentials"

    displayName = m.nameTextBox.text
    if displayName = invalid or displayName = "" then return invalid

    email = m.emailTextBox.text
    if email = invalid or email = "" then return invalid

    password = m.passwordTextBox.text
    if password = invalid or password = "" then return invalid

    return { "displayName": displayName, "email": email, "password": password }

End Function






Sub OnSignUpTask_DataAvailable()

    print "SignUpView:OnSignUpTask_DataAvailable"

    m.signUpButton.setFocus(true)
    m.busySpinner.visible = false
    
    result = m.signUpTask.data

    ' This only fires on error
    ' Other events are handled in the task

    ShowControls(false)
    m.errorDialog.message = [result.error]
    m.top.getScene().dialog = m.errorDialog

End Sub



Function onKeyEvent(key as String, press as Boolean) as Boolean

    print "SignUpView:OnKeyEvent", key, press
    
    if not press then return false

    if key = "back" then return true

    if key = "up" then
        if m.emailTextBox.hasFocus() then
            FocusOnTB(m.nameTextBox)
            return true
        else if m.passwordTextBox.hasFocus() then
            FocusOnTB(m.emailTextBox)
            return true
        else if m.signUpButton.hasFocus() then
            FocusOnTB(m.passwordTextBox)
            return true
        else if m.loginAccountButton.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then
                FocusOnTB(m.passwordTextBox)
            else
                FocusOnButton(m.signUpButton)
            end if
            return true
        end if
    end if

    if key = "down" then
        if m.nameTextBox.hasFocus() then
            FocusOnTB(m.emailTextBox)
            return true
        else if m.emailTextBox.hasFocus() then
            FocusOnTB(m.passwordTextBox)
            return true
        else if m.passwordTextBox.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then 
                FocusOnButton(m.loginAccountButton)
                return true
            else
                FocusOnButton(m.signUpButton)
                return true
            end if
        else if m.signUpButton.hasFocus() then
            FocusOnButton(m.loginAccountButton)
            return true
        end if
    end if

    
    if key = "OK" then 

        if m.nameTextBox.hasFocus() or m.emailTextBox.hasFocus() or m.passwordTextBox.hasFocus() then

            if m.nameTextBox.hasFocus() then
                m.keyboardDialog.title = "Name:"
                m.keyboardDialog.keyBoardDomain = "generic"
            else if m.emailTextBox.hasFocus() then
                m.keyboardDialog.title = "Email Address:"
                m.keyboardDialog.keyBoardDomain = "email"
            else
                m.keyboardDialog.title = "Password:"
                m.keyboardDialog.keyBoardDomain = "password"
            end if

            m.keyboardDialog.text = m.lastSelected.text
            m.keyboardDialog.textEditBox.secureMode = m.passwordTextBox.hasFocus()
            

            ShowControls(false)
            m.top.getScene().dialog = m.keyboardDialog
            return true

        end if

        ' Return button click sound
        if m.loginAccountButton.hasFocus() or m.signUpButton.hasFocus() return true

    end if

    return false

End Function
