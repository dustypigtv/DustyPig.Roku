sub Init()

    print "SignupView:Init"

    ' Find controls    
    m.headerLabel = m.top.findNode("headerLabel")
    m.nameTextBox = m.top.findNode("nameTextBox")
    m.emailTextBox = m.top.findNode("emailTextBox")
    m.passwordTextBox = m.top.findNode("passwordTextBox")
    m.loginButton = m.top.findNode("loginButton")
    m.signupButton = m.top.findNode("signupButton")
    m.busySpinner = m.top.findNode("busySpinner")


    ' Init controls
    m.headerLabel.color = m.global.defaultTheme.textOnBackground
    m.headerLabel.visible = true

    m.busySpinner.poster.uri = m.global.defaultTheme.spinner

    m.loginButton.focusBitmapUri = m.global.defaultTheme.primaryBitmap
    m.loginButton.observeField("buttonSelected", "OnLoginClicked")

    m.signupButton.focusBitmapUri = m.global.defaultTheme.primaryBitmap
    m.signupButton.observeField("buttonSelected", "OnSignupClicked")



    
    ' Build keyboard dialog
    m.keyboardDialog = createObject("roSGNode", "StandardKeyboardDialog")
    m.keyboardDialog.buttons=["Cancel","OK"]
    m.keyboardDialog.observeField("wasClosed", "OnKeyboardDialogClosed")
    m.keyboardDialog.observeField("buttonSelected", "OnKeyboardDialogButtonSelected")


    ' Error dialog
    m.errorDialog = createObject("roSGNode", "StandardMessageDialog")
    m.errorDialog.buttons = ["OK"]
    m.errorDialog.title = "Sign up error"
    m.errorDialog.observeField("buttonSelected", "OnErrorDialogClosed")
    m.errorDialog.observeField("wasClosed", "OnErrorDialogClosed")


    ' Signup task
    m.signupTask = CreateObject("roSGNode", "SignupTask")
    m.signupTask.observeField("data", "OnSignupResult")


    'variables
    m.lastSelected = invalid
    m.alreadyAskedChannelStore = false


    ' Observe this.visible
    m.top.observeField("visible", "OnVisibleChanged")

end sub


' Fires when this view's visibility changes
Sub OnVisibleChanged(event as object)

    print "SignupView:OnVisibleChanged"

    m.busySpinner.visible = false
    if not event.getData() then return
    m.lastSelected = m.nameTextBox
    ShowControls(true)

    ' Test user
    appInfo = CreateObject("roAppInfo")
    if appInfo.isDev()

        m.nameTextBox.text = "New Test User"
        m.emailTextBox.text = "newtestuser@dustypig.tv"
        m.passwordTextBox.text = "test password"

    else if m.alreadyAskedChannelStore = false

        m.alreadyAskedChannelStore = true
        m.channelStore = CreateObject("roSGNode", "ChannelStore")
        
        ' Set sign-in context for RFI screen
        info = CreateObject("roSGNode", "ContentNode")
        info.addFields({context: "signup"})
        m.channelStore.requestedUserDataInfo = info
        
        m.channelStore.requestedUserData = "firstName, email"
        m.channelStore.observeField("userData", "OnChannelStoreGetInfo")
        m.channelStore.command = "getUserData"

    end if

End Sub


sub OnChannelStoreGetInfo(data as object)

    print "SignupView:OnChannelStoreGetInfo"

    if m.channelStore.userData <> invalid then
        m.nameTextBox.text = m.channelStore.userData.firstName
        m.emailTextBox.text = m.channelStore.userData.email
        FocusOnTB(m.passwordTextBox)
    end if

end sub


Sub ShowControls(s as boolean)

    print "SignupView:ShowControls "; s

    if not s then

        if m.nameTextBox.hasFocus() then m.lastSelected = m.nameTextBox
        if m.emailTextBox.hasFocus() then m.lastSelected = m.emailTextBox
        if m.passwordTextBox.hasFocus() then m.lastSelected = m.passwordTextBox

    end if

    m.headerLabel.visible = s
    m.nameTextBox.visible = s
    m.emailTextBox.visible = s
    m.passwordTextBox.visible = s
    m.loginButton.visible = s
    m.signupButton.visible = s

    if s then FocusOnTB(m.lastSelected)

End Sub


Sub FocusOnTB(tb as object)

    print "SignupView:FocusOnTB"

    m.nameTextBox.textColor = "#808080"
    m.emailTextBox.textColor = "#808080"
    m.passwordTextBox.textColor = "#808080"

    if tb = invalid then tb = m.nameTextBox

    tb.setFocus(true)
    tb.textColor = m.global.defaultTheme.primary

End Sub


Sub FocusOnButton(btn as object)

    print "SignupView:FocusOnButton"

    m.emailTextBox.textColor = "#808080"
    m.passwordTextBox.textColor = "#808080"

    if btn = invalid then btn = m.signupButton

    btn.setFocus(true)

End Sub


Sub OnKeyboardDialogClosed(event as object)

    print "SignupView:OnKeyboardDialogClosed"

    ShowControls(true)

End Sub


Sub OnKeyboardDialogButtonSelected(event as object)

    print "SignupView:OnKeyboardDialogButtonSelected"

    m.top.getScene().dialog = invalid
    idx = event.getData()
    selectSignup = false
    if idx = 1 then 
        m.lastSelected.text = m.keyboardDialog.text
        if m.lastSelected.id = m.emailTextBox.id and m.emailTextBox.text = "newtestuser@dustypig.tv" then 
            m.nameTextBox.text = "New Test User"
            m.passwordTextBox.text = "test password"
            selectSignup = true
        end if
    end if
    ShowControls(true)
    if selectSignup then FocusOnButton(m.signupButton)

End Sub

Sub OnErrorDialogClosed()

    print "SignupView:OnErrorDialogClosed"

    m.top.getScene().dialog = invalid
    ShowControls(true)

End Sub


Sub OnLoginClicked()

    print "SignupView:OnLoginClicked"

    m.top.switchToLogin = true

End sub


Sub OnSignupClicked()

    print "SignupView:OnSignupClicked"

    m.signupButton.setFocus(false)
    m.busySpinner.visible = true

    credentials = GetCredentials()
    m.signupTask.displayName = credentials.displayName
    m.signupTask.email = credentials.email
    m.signupTask.password = credentials.password
    m.signupTask.control = "RUN"

End Sub

Function GetCredentials() as object

    print "LoginVSignupViewiew:GetCredentials"

    displayName = m.nameTextBox.text
    if displayName = invalid or displayName = "" then return invalid

    email = m.emailTextBox.text
    if email = invalid or email = "" then return invalid

    password = m.passwordTextBox.text
    if password = invalid or password = "" then return invalid

    return { "displayName": displayName, "email": email, "password": password }

End Function






Sub OnSignupResult()

    print "SignupView:OnSignupResult"

    m.signupButton.setFocus(true)
    m.busySpinner.visible = false
    
    data = m.signupTask.data
    if data.success then

        if data.loginType = 1 then '1 = Main Profile in api
            m.top.switchToHomeScreen = true
        else
            m.top.switchToSelectProfile = true
        end if

    else

        ShowControls(false)
        m.errorDialog.message = [data.error]
        m.top.getScene().dialog = m.errorDialog

    end if

End Sub



Function onKeyEvent(key as String, press as Boolean) as Boolean

    print "SignupView:onKeyEvent " + key + " "; press

    if not press then return false

    if key = "back" then return true

    if key = "up" then
        if m.emailTextBox.hasFocus() then
            FocusOnTB(m.nameTextBox)
            return true
        else if m.passwordTextBox.hasFocus() then
            FocusOnTB(m.emailTextBox)
            return true
        else if m.signupButton.hasFocus() then
            FocusOnTB(m.passwordTextBox)
            return true
        else if m.loginButton.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then
                FocusOnTB(m.passwordTextBox)
            else
                FocusOnButton(m.signupButton)
            end if
            return true
        end if
    end if

    if key = "down" then
        if m.nameTextBox.hasFocus() then
            FocusOnTB(m.emailTextBox)
            return true
        else if m.emailTextBox.hasFocus() then
            FocusOnTB(m.passwordTextBox)
            return true
        else if m.passwordTextBox.hasFocus() then
            credentials = GetCredentials()
            if credentials = invalid then 
                FocusOnButton(m.loginButton)
                return true
            else
                FocusOnButton(m.signupButton)
                return true
            end if
        else if m.signupButton.hasFocus() then
            FocusOnButton(m.loginButton)
            return true
        end if
    end if

    
    if key = "OK" then 

        if m.emailTextBox.hasFocus() or m.passwordTextBox.hasFocus() then

            if m.emailTextBox.hasFocus() then
                m.keyboardDialog.title = "Email Address:"
                m.keyboardDialog.keyBoardDomain = "email"
            else
                m.keyboardDialog.title = "Password:"
                m.keyboardDialog.keyBoardDomain = "password"
            end if

            m.keyboardDialog.text = m.lastSelected.text
            m.keyboardDialog.textEditBox.secureMode = m.passwordTextBox.hasFocus()
            

            ShowControls(false)
            m.top.getScene().dialog = m.keyboardDialog
            return true

        end if

        ' Return button click sound
        if m.loginButton.hasFocus() or m.signupButton.hasFocus() return true

    end if

    return false

End Function
