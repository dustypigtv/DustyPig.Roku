Sub Init()

    m.top.notificationInterval = 0.5 ' Shortest allowed
    m.lastPosition = 0.0

    m.updatePlaybackProgressTask = CreateObject("roSGNode", "UpdatePlaybackProgressTask")
    m.markMovieWatchedTask = createObject("roSGNode", "MarkMovieWatchedTask")

    m.top.observeField("state", "OnThis_StateChanged")
    m.top.observeField("position", "OnThis_PositionChanged")
    m.top.observeField("errorStr", "OnThis_Error")
    
End sub


Sub OnThis_StateChanged(event as object)

    ' none	        No current play state
    ' buffering     Video stream is currently buffering
    ' playing	    Video is currently playing
    ' paused	    Video is currently paused
    ' stopping      Available since Roku OS 12.5. Video is in the process of being stopped. This value is only returned if the asyncStopSemantics field is enabled.
    ' stopped	    Video is currently stopped
    ' finished	    Video has successfully completed playback
    ' error	        An error has occurred in the video play. The error code, message, and diagnostics can be found in the errorCode, errorMsg, and errorStr fields respectively.    

    if m.top.content = invalid then return

    state = event.getData()

    print "VideoView:Video.State = " + state

    if state = "finished" then
        
        ' Set contentType with a string, read as a number. Strange.
        ' movie = 1
        if m.top.content.contentType = 1 then
            m.updatePlaybackProgressTask.control = "stop"
            m.markMovieWatchedTask.id = m.top.content.id
            m.markMovieWatchedTask.control = "run"
            m.global.removeFromContinueWatching = m.top.content.id
        end if

    end if


End Sub


Sub OnThis_PositionChanged(event as object)

    ' if not m.top.visible then return
    ' if m.top.content = invalid then return
    ' if m.global.currentMedia = invalid then return
    ' if m.top.content.id <> m.global.currentMedia.id then return
    ' if m.top.state = "finished" then return

    position = event.getData()
    print "VideoView:Postion", position
    
    m.global.playbackPosition = position
    
    if m.lastPosition <= position - 10 or m.lastPosition > position then
        
        m.lastPosition = position

        m.updatePlaybackProgressTask.control = "stop"
        m.updatePlaybackProgressTask.id = m.top.content.id
        m.updatePlaybackProgressTask.seconds = position
        m.updatePlaybackProgressTask.control = "run"
        m.global.addToContinueWatching = BuildBasicMediaFromDetailed(m.top.content)

    end if

End Sub



Sub OnThis_Error(event as object)

    m.top.control = "stop"
    print event.getData()

End Sub


