 Sub Main(args)

    'Create Message port
    m.port = CreateObject("roMessagePort") 
    
    'Create Screen object
    screen = CreateObject("roSGScreen") 
    screen.setMessagePort(m.port) 


    m.global = screen.getGlobalNode()
    
    ' Profile data will be memory only
    m.global.addField("allProfiles", "node", false)
    m.global.addFields({profileId:0, profileToken:"", profileIsMain:false})
    

    ' Setup theme
    m.global.addFields({busySpinnerUri:"pkg:/images/spinner.png", "theme":GetTheme(), "defaultTheme":GetTheme()})
    
    
    'Create Base Scene
    screen.CreateScene("BaseScene") 

    ' Handle deep link
    m.global.addField("deeplink", "assocarray", false)
    m.global.deeplink = GetDeepLinks(args)

    screen.show()
    ' vscode_rdb_on_device_component_entry


    'Main program loop
    while(true) 

        msg = wait(0, m.port)
        msgType = type(msg)
        
        if msgType = "roSGScreenEvent"
        
            if msg.isScreenClosed() then return

        end if

    end while

end sub


Function GetDeepLinks(args) as Object

    if args.contentid <> Invalid and args.mediaType <> Invalid
        return {
            id: args.contentId
            type: args.mediaType
        }
    end if

    return invalid
    
end Function

